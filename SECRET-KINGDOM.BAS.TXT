1 CONSOLE0,25:COLOR,,4,0:CLR:Z$="OBVIOUS EXITS:- ":I=9:A=1:B=2:GOTO10
2 FORY=1TO500:NEXT:GOTO30
3 FORY=1TO2400:NEXT:GOTO30
4 FORY=1TO4500:NEXT:GOTO30
5 FORY=1TO4500:NEXT:RETURN
6 PRINT"IT IS";B$(C):GOTO3
7 IF(NL=1)*(P(20)=1)THENB$(20)=" A TORCH ":NL=0
8 GOTO2
9 FORY=1TO50:NEXT:RETURN
10 DIMP(100),B$(100),T(100),C$(4):GOTO503
11 FORX=0TO4:READC$(X):NEXT
12 FORX=1TO53:READB$(X):NEXT
13 FORX=1TO53:READP(X):NEXT
14 FORX=1TO53:READT(X):NEXT
15 DATAGO
16 DATAGETINVUSEDIGHIT
17 DATATAKEDROPDIVEHOLDLOOKSAVEQUITGIVEOPENKILLPULLWEARFILLPOURREADBURN
18 DATAENTERLEAVESMELLTHROWSHOOTLIGHTCLIMBSCORE
19 DATAREMOVE
20 D$="NORSOUEASWESDOWUP "
21 E$="LAKLANISLDOOTOWWINWELHOLPITLADTRE"
22 B1$="MANORCSWOSTASKEMEDSNASNOMAIGOBRINHORCROOILJEWBALCLOCHEHAMTORBOXFLI"
23 B2$="WATHOLISLSHOTRESNODIACARSTULAKWIZFIRRAGDOOENGSTOPARROPLADBOWARRBIR"
24 B3$="BAGWELKEYBRAWOLWINLEVFIGLAN"
25 B$=B1$+B2$+B3$:B1$="":B2$="":B3$=""
26 K$="WOLORCSNAWIZBIRMAN"
27 U$="STAKEYWATHAMRAGSWO"
28 S$="BIRMANORCSNAWIZWOL"
29 GOTO506
30 COLOR,,4,0:CONSOLE0,25:PRINT"You are standing ";:V=0:IF(I>2)*(I<15)GOSUB413
31 ONI-14GOSUB424,426,428,430,432,433,435,437,439,441,443,445
32 ONI-26GOSUB445,447,449,451,452,453,455,457,459,461,462,465
33 ONI-38GOSUB467,470,472,474,476,478,481,484,486
34 N=N+1:IFN>19THENN=N:IF(I=30)-(I=31)-(I=32)-(I=43)-(I=45)THEN40
35 IF(N>19)*(RND(1)<.9)THENP(49)=I
36 IF(P(49)=I)*(RND(1)<.01)THEN588
37 IF(I=30)*(P(14)<>2)THENI=I:IFRND(1)<.2THEN591
38 IF(NL=1)*(P(14)=2)THEN595
39 IF((I=26)-(I=27))*(P(17)<>2)THENI=I:IFRND(1)<.2THEN598
40 PRINT"VISIBLE OBJECTS ARE:-":FORX=1TO53:IFP(X)=ITHENPRINTB$(X);:V=1
41 NEXT:IFV=0THENPRINT"NO VISIBLE OBJECTS"
42 COLOR,,2,0:PRINT:PRINT"       >------------------------<":PZ=PEEK(85):CONSOLEPZ,25-PZ:IF(BH<>0)*(I<>32)GOSUB121
43 COLOR,,5,0:PRINT"What do you want to do?":X$="":COLOR,,7,0:PRINT">ƒ";:N$=""
44 GETN$:IFN$=""GOTO44
45 T=ASC(N$):L=LEN(X$)
46 IF(T=20)*(L>0)*(RIGHT$(X$,1)=" ")THENSP=0
47 IF(T=20)*(L>0)THENX$=LEFT$(X$,L-1):PRINT"ƒ ";:GOTO44
48 IF(T=13)*(L>1)THENPRINT" ";:SP=0:PRINT:COLOR,,6,0:GOTO53
49 IF(T=32)*(SP=0)*(L>1)THENSP=1:GOTO51
50 IF(T<65)-(T>90)THENUSR(62):GOTO44
51 IFL=20THENUSR(62):GOTO44
52 X$=X$+N$:PRINT"";N$;"ƒ";:GOTO44
53 FORF=1TOL:IFMID$(X$,F,1)=" "THENF=F-1:GOTO57
54 NEXT:IF(L>1)*(L<6)THENSW=1:F=L:GOTO57
55 PRINT"I CANNOT MAKE ANY SENSE OF"
56 PRINT"'";X$;"'":GOTO43
57 IFF>6THEN61
58 Y=LEN(C$(F-2))
59 FORX=1TOYSTEPF:IFLEFT$(X$,F)=MID$(C$(F-2),X,F)THENC=(X+F-1)/F:GOTO63
60 NEXT
61 PRINT"I MUST BE STUPID BUT I DO NOT UNDERSTAND"
62 PRINT"WHAT YOU MEAN BY '";X$;"'":GOTO43
63 ONF-1GOTO64,65,66,68,69
64 ONCGOTO70
65 ONCGOTO138,145,319,342,130
66 ONCGOTO138,149,114,117,168,547,569,156,359,203,165,364,370,123,125
67 ONC-15GOTO129
68 ONCGOTO76,149,134,354,384,393,110,213
69 ONCGOTO345
70 FORX=1TO16STEP3:IFMID$(D$,X,2)=MID$(X$,F+2,2)THENC=(X+2)/3:GOTO72
71 NEXT:GOTO61
72 IF(I>2)*(I<15)THEN218
73 ONI-14GOTO224,227,231,234,237,241,244,247,250,254,258,262,267
74 ONI-27GOTO272,276,223,223,278,280,284,288,291,295,298,300,304
75 ONI-40GOTO306,308,309,311,313,315,317
76 FORX=1TO35STEP3:IFMID$(E$,X,3)=MID$(X$,F+2,3)THENC=(X+2)/3:GOTO78
77 NEXT:PRINT"SORRY THAT'S NOT POSSIBLE":GOTO43
78 ONCGOTO79,82,84,86,88,94,97,100,102,105,108
79 IFI=31THENI=30:PRINT"O.K.":GOTO7
80 IF(I<>20)*(I>15)*(I<25)THENII=I:I=30:PRINT"O.K.":GOTO7
81 PRINT"YOU ARE NOT NEAR IT":GOTO43
82 IFI=30THENI=II:PRINT"O.K.":GOTO2
83 PRINT"YOU ARE ON LAND ALREAY":GOTO43
84 IFI=30THENI=31:PRINT"O.K.":GOTO2
85 PRINT"YOU ARE NOT NEAR IT":GOTO43
86 IFI=29THEN92
87 IFI=33THENI=29:PRINT"O.K.":GOTO2
88 IF(I=38)*(HM=1)THENI=39:PRINT"O.K.":GOTO2
89 IFI=38THENPRINT"MAN WON'T LET YOU":GOTO43
90 PRINT"YOU ARE NOT NEAR IT":GOTO43
91 PRINT"WHAT DOOR":GOTO43
92 IFD1=0THENPRINT"IT'S SOLID STONE YOU CANNOT ENTER IT!":GOTO43
93 IFD1=1THENI=33:PRINT"O.K.":GOTO2
94 IFI=42THENI=41:PRINT"O.K.":GOTO2
95 IF(I=41)*(P(50)=I)THENI=42:PRINT"O.K":GOTO2
96 PRINT"I DO NOT SEE ONE !":GOTO43
97 IFI-4THEN81
98 IFP(40)=100THENI=37:PRINT"O.K.":GOTO2
99 PRINT"YOU CAN'T DO THAT AT THE MOMENT":GOTO43
100 IFP(24)=ITHENI=43:PRINT"O.K.":GOTO2
101 PRINT"THE ONLY HOLE I CAN SEE IS THE ONE BETWEEN YOUR EARS":GOTO43
102 IFI<>35THEN96
103 IFNL=0THEN581
104 I=44:PRINT"THE SNAKES KEEP AWAY FROM YOUR TORCH":GOTO43
105 IFP(41)<>ITHENPRINT"SHE IS NOT HERE":GOTO43
106 IFP(9)<>2THEN577
107 PRINT"THE CHAIN MAIL PROTECTS YOU":I=45:GOTO3
108 IFI=38THENI=46:PRINT"O.K.":GOTO2
109 GOTO81
110 IFMID$(X$,F+2,3)="TRE"THEN112
111 PRINT"SORRY THAT'S NOT POSSIBLE":GOTO43
112 IFI<>38THENPRINT"YOU CANNOT HERE":GOTO43
113 PRINT"O.K.":I=47:GOTO2
114 IF(I=30)*(BH>0)THENI=32:GOTO30
115 IFI=30THEN567
116 PRINT"THAT WILL DO NO GOOD HERE":GOTO43
117 IFMID$(X$,6,3)="BRE"THEN119
118 PRINT"THAT WON'T DO ANY GOOD":GOTO43
119 IFI=30THENBH=1:PRINT"O.K.":GOTO43
120 PRINT"NOT HERE":GOTO43
121 BH=BH+1:IFBH>3THENPRINT"YOU GASP FOR BREATH":BH=0:RETURN
122 RETURN
123 IFMID$(X$,F+2,3)="WAT"THENPRINT"Try the USE command":GOTO43
124 PRINT"You make little sense":GOTO43
125 IFMID$(X$,F+2,3)="ENG"THEN127
126 PRINT"THAT'S NOT POSSIBLE":GOTO43
127 IFI<>40THENPRINT"I DON'T SEE IT":GOTO43
128 PRINT"IT SAYS'STORE TREASURE IN A HOLLOW'":GOTO43
129 PRINT"I'M NOT AN ARSONIST":GOTO43
130 IFMID$(X$,F+2,3)="FIG"THEN132
131 PRINT"THAT WILL DO NO GOOD":GOTO43
132 IFP(52)=ITHEN207
133 PRINT"I DON'T SEE THEM":GOTO43
134 IFMID$(X$,F+2,2)="OI"THENC=14:GOTO136
135 PRINT"THAT WILL DO NO GOOD":GOTO43
136 IF(P(C)=2)-(P(C)=I)THENPRINT"SMELLS LIKE A FIERCE FISH":GOTO43
137 PRINT"I CAN'T EVEN SEE IT!!":GOTO43
138 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO140
139 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
140 IN=0:FORX=1TO48:IFP(X)=1THENIN=IN+1
141 NEXT:IFIN>5THENPRINT"YOU'RE CARRYING TOO MUCH":GOTO43
142 IFP(C)<>ITHENPRINT"I DO NOT SEE IT HERE":GOTO43
143 IFT(C)<>0THEN400
144 P(C)=1:IN=IN+1:PRINT"O.K.":GOTO2
145 CONSOLE0,25:PRINT"YOU ARE CARRYING THE FOLLOWING :-"
146 FORX=1TO53:IFP(X)=1THENPRINT"";B$(X)
147 NEXT:FORX=1TO53:IFP(X)=2THENPRINT"YOU ARE WEARING";B$(X)
148 NEXT:FORX=1TO(IN+2)*600:NEXT:GOTO30
149 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO151
150 NEXT:PRINT"I DO NOT THINK THERE IS ONE":GOTO43
151 IFP(C)=1THENP(C)=I:PRINT"O.K.":GOTO153
152 PRINT"YOU DO NOT HAVE THAT":GOTO43
153 IFI<>47THEN2
154 IFC<>16THENP(C)=25:GOTO2
155 B$(1)=" A HEADLESS MAN IN ARMOUR ":HM=1:P(16)=0:GOTO2
156 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO158
157 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
158 IFP(C)<>1THENPRINT"I DO NOT THINK YOU HAVE IT":GOTO43
159 IFI=16THEN161
160 PRINT"NOT HERE":GOTO43
161 IFC=34THEN163
162 PRINT"WIZARD IGNORES YOU":GOTO43
163 PRINT"WIZARD TAKES THEM AND DROPS SOMETHING":P(34)=0:P(33)=0:P(4)=16
164 PRINT"ON LEAVING":GOTO3
165 IF(I=39)*(MID$(X$,F+2,3)="LEV")THENSC=1:GOTO43
166 IF(I-39)*(MID$(X$,F+2,3)="LEV")THENPRINT"NOT HERE":GOTO43
167 PRINT"PULL YOURSELF A PINT INSTEAD":GOTO43
168 FORX=1TO160:IFMID$(X$,6,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO170
169 NEXT:PRINT"I DO NOT THINK THERE IS A ";RIGHT$(X$,LEN(X$)-5):GOTO43
170 IF(P(C)=I)-(P(C)=1)THEN172
171 PRINT"I CAN'T SEE IT HERE":GOTO43
172 ONCGOTO177,178,201,181,186,192,6,6,187,192,192,192,192,188,192,189,6
173 ONC-17GOTO6,6,6,6,6,6,6,190,6,191,6,192,192,192,6,176
174 ONC-33GOTO179,6,183,193,6,194,6,195,6,6,196,184,6,6,192,197
175 ONC-49GOTO6,6,198,6,6
176 PRINT"HE LOOKS LIKE HE WANTS SOMETHING":GOTO43
177 PRINT"HE LOOKS MENACING":GOTO43
178 PRINT"THEY DON'T LOOK FRIENDLY":GOTO43
179 PRINT"THEY ARE THE BEST IN THE ANCIENT WORLD"
180 PRINT"SUITABLE FOR ONLY THE FINEST HANDS":GOTO43
181 IFP(2)=ITHENPRINT"STAFF GLOWS RED HOT":GOTO43
182 PRINT"IT HAS A STRANGE POWER":GOTO43
183 PRINT"IT LOOKS LIKE IT'S MADE OF SALT":GOTO43
184 IFFB=1THENPRINT"IT'S A FULL BAG":GOTO43
185 GOTO6
186 PRINT"LOOKS DEAD!":GOTO43
187 PRINT"IT'S IN GOOD CONDITION":GOTO43
188 PRINT"TRY SMELLING IT":GOTO43
189 PRINT"WOULD NOT LIKE THAT TO FALL ON ME":GOTO43
190 PRINT"LOOKS LIKE A TREASURE ISLAND":GOTO43
191 PRINT"IT'S LARGE ENOUGH TO BE HOLLOW":GOTO43
192 PRINT"IT LOOKS LIKE TREASURE":GOTO43
193 PRINT"TRY READING IT":GOTO43
194 PRINT"LOOKS WORTHLESS":GOTO43
195 PRINT"LOOKS LIKE IT HAS A HIDDEN SECRET":GOTO43
196 GOTO199
197 PRINT"LOOKS FIERCE":GOTO43
198 PRINT"THEY HAVE UGLY FACES":GOTO43
199 IFP(11)<>0THENPRINT"IT'S A DEAD BIRD":GOTO43
200 PRINT"SOMETHING GLITTERS IN ITS BEAK":GOTO43
201 IFP(31)<>0THEN6
202 PRINT"YOU'VE FOUND SOMETHING":P(31)=I:GOTO3
203 FORX=1TO16STEP3:IFMID$(X$,F+2,3)=MID$(K$,X,3)THENC=(X+2)/3:GOTO205
204 NEXT:PRINT"YOU WOULD KILL ANYTHING ON SIGHT":GOTO43
205 ONCGOTO211,206,208,574,209,210
206 IFP(2)<>ITHENPRINT"I DON'T SEE THEM":GOTO43
207 PRINT"WHAT WILL YOU USE (e.g. USE HANDS)":GOTO43
208 PRINT"THERE ARE TOO MANY":GOTO43
209 PRINT"YOU WOULD HAVE TO SHOOT IT":GOTO43
210 PRINT"IT'S NOT THAT EASY":GOTO43
211 IFP(49)<>ITHENPRINT"I DON'T SEE IT":GOTO43
212 GOTO207
213 SS=-10*((P(6)=46)+(P(10)=46)+(P(11)=46)+(P(12)=46)+(P(13)=46)+(P(15)=46))
214 SS=SS-10*((P(48)=46)+(P(29)=46)+(P(30)=46)+(P(31)=46))
215 IFSS<100THENPRINT"YOU HAVE SCORED";SS;" POINTS OUT OF 100":GOTO43
216 CONSOLE0,25:PRINT"CONGRATULATIONS YOU HAVE SOLVED"
217 PRINT" THIS ADVENTURE. WOULD YOU LIKE TO":GOTO601
218 IF(C=1)*(I=12)THENI=15:GOTO30
219 IF(C=1)*(A<2)THENA=A+1:I=I+4:GOTO30
220 IF(C=3)*(B<3)THENB=B+1:I=I+1:GOTO30
221 IF(C=4)*(B>0)THENB=B-1:I=I-1:GOTO30
222 IF(C=2)*(A>0)THENA=A-1:I=I-4:GOTO30
223 PRINT"YOU CANNOT DO THAT":GOTO3
224 IFC=1THENI=16:GOTO30
225 IFC=2THENI=12:GOTO30
226 GOTO223
227 IFC=2THENI=15:GOTO30
228 IFC=3THENI=17:GOTO30
229 IFC=4THENI=18:GOTO30
230 GOTO223
231 IFC=1THENI=19:GOTO30
232 IFC=4THENI=16:GOTO30
233 GOTO223
234 IFC=1THENI=20:GOTO30
235 IFC=3THENI=16:GOTO30
236 GOTO223
237 IFC=1THENI=21:GOTO30
238 IFC=2THENI=17:GOTO30
239 IFC=3THENI=22:GOTO30
240 GOTO223
241 IFC=1THENI=23:GOTO30
242 IFC=2THENI=18:GOTO30
243 GOTO223
244 IFC=2THENI=19:GOTO30
245 IFC=4THENI=24:GOTO30
246 GOTO223
247 IFC=6THENI=28:GOTO30
248 IFC=4THENI=19:GOTO30
249 GOTO223
250 IFC=3THENI=24:GOTO30
251 IFC=2THENI=20:GOTO30
252 IFC=6THENI=38:GOTO30
253 GOTO223
254 IFC=1THENI=25:GOTO30
255 IFC=3THENI=21:GOTO30
256 IFC=4THENI=23:GOTO30
257 GOTO223
258 IFC=3THENI=26:GOTO30
259 IFC=4THENI=27:GOTO30
260 IFC=2THENI=24:GOTO30
261 GOTO223
262 IF(C=1)*(D<6)THEND=D+1:GOTO30
263 IF(C=2)*(D>0)THEND=D-1:GOTO30
264 IFC=1THEND=0:I=21:GOTO30
265 IFC=2THEND=D-1:I=25:GOTO30
266 GOTO223
267 IF(C=1)*(D<6)THEND=D+1:GOTO30
268 IF(C=2)*(D>0)THEND=D-1:GOTO30
269 IFC=1THEND=0:I=23:GOTO30
270 IFC=2THEND=D-1:I=25:GOTO30
271 GOTO223
272 IFC=5THENI=22:GOTO30
273 IF(C=6)*(P(2)=28)THENPRINT"CAN'T THE ORCS WON'T LET YOU":GOTO43
274 IFC=6THENI=29:GOTO30
275 GOTO223
276 IFC=5THENI=28:GOTO30
277 GOTO223
278 IFC=6THENI=30:GOTO30
279 GOTO223
280 IF(NL=0)*(RND(1)>.7)THEN585
281 IFC=5THENI=34:GOTO30
282 IFC=4THENI=35:GOTO30
283 GOTO223
284 IF(NL=0)*(RND(1)>.7)THEN585
285 IFC=6THENI=33:GOTO30
286 IFC=2THENI=36:GOTO30
287 GOTO223
288 IF(NL=0)*(RND(1)>.7)THEN585
289 IFC=3THENI=33:GOTO30
290 GOTO223
291 IF(NL=0)*(RND(1)>.7)THEN585
292 IFC=1THENI=34:GOTO30
293 IFC=2THENI=37:GOTO30
294 GOTO223
295 IFC=1THENI=36:GOTO30
296 IF(C=6)*(P(40)=100)THENI=4:B=1:A=0:GOTO30
297 GOTO223
298 IFC=5THENI=23:GOTO30
299 GOTO223
300 IFC=5THENI=40:GOTO30
301 IFC=2THENI=38:GOTO30
302 IF(C=6)*(SC=1)THENI=41:GOTO30
303 GOTO223
304 IFC=6THENI=39:GOTO30
305 GOTO223
306 IFC=5THENI=39:GOTO30
307 GOTO223
308 GOTO223
309 IFC=6THENI=31:GOTO30
310 GOTO223
311 IFC=6THENI=35:GOTO30
312 GOTO223
313 IFC=1THENI=5:GOTO30
314 GOTO223
315 IFC=4THENI=38:GOTO30
316 GOTO223
317 IFC=5THENI=38:GOTO30
318 GOTO223
319 FORX=1TO16STEP3:IFMID$(X$,F+2,3)=MID$(U$,X,3)THENC=(X+2)/3:GOTO321
320 NEXT:PRINT"YOU CAN'T DO THAT":GOTO43
321 ONCGOTO322,325,329,332,336,339
322 IFP(4)<>1THENPRINT"YOU DO NOT HAVE THE STAFF":GOTO43
323 IFP(2)=28THENPRINT"O.K.":P(2)=0:P(52)=28:P(4)=0:P(14)=I:GOTO2
324 PRINT"NOT HERE":GOTO43
325 IFI<>41THENPRINT"NOT HERE":GOTO43
326 IFP(15)<>0THENPRINT"THE CHEST IS NOT LOCKED":GOTO43
327 IFP(47)<>1THENPRINT"YOU DO NOT HAVE THE KEY":GOTO43
328 PRINT"O.K.":P(19)=I:P(15)=I:B$(18)=" OPEN TREAURE CHEST ":GOTO2
329 IFI<>29THENPRINT"NOT HERE":GOTO43
330 IF(FB=1)*(P(45)=1)THENPRINT"PART OF THE DOOR DISOLVES":FB=0:D1=1:GOTO43
331 PRINT"YOU HAVE NO WATER":GOTO43
332 IFP(19)<>1THENPRINT"YOU DO NOT HAVE THE HAMMER":GOTO43
333 IFI<>28THENPRINT"NOT HERE":GOTO43
334 IFP(52)<>ITHENPRINT"IT WILL DO NO GOOD":GOTO43
335 PRINT"O.K.":P(52)=100:P(48)=I:P(38)=I:GOTO2
336 IFP(35)<>1THENPRINT"WHAT RAGS!":GOTO43
337 IFP(14)<>2THENPRINT"YOU DON'T NEED TO AT THE MOMENT":GOTO43
338 P(14)=100:PRINT"O.K.":GOTO2
339 IFP(3)<>1THENPRINT"YOU DO NOT HAVE IT":GOTO43
340 IFP(49)<>ITHENPRINT"YOU DON'T NEED TO":GOTO43
341 N=0:P(49)=0:PRINT"O.K.":GOTO2
342 IFI<>31THENPRINT"NOT HERE":GOTO43
343 IFP(26)<>1THENPRINT"YOU DO NOT HAVE A SHOVEL":GOTO43
344 PRINT"O.K.":P(24)=I:GOTO2
345 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO347
346 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
347 IF(I=41)*(C=39)THENT(39)=0:P(50)=I:GOTO30
348 IF((P(5)=1)-(P(5)=I))*(P(9)=0)THENB$(5)=" A SKELETON ":P(9)=I:GOTO352
349 IF(P(9)=2)*(C=9)THENP(9)=1:GOTO353
350 IF(P(14)=2)*(C=14)THENPRINT"HOW ( i.e. USE HANDS) ? ":GOTO43
351 PRINT"YOU ARE MAKING LITTLE SENSE":GOTO43
352 T(9)=0:PRINT"O.K.":GOTO2
353 PRINT"O.K.":GOTO43
354 IFMID$(X$,F+2,2)="RO"THEN356
355 PRINT"I DO NOT SEE THE POINT IN THAT":GOTO43
356 IFP(40)-1THENPRINT"YOU DO NOT HAVE THE ROPE":GOTO43
357 IF(I=4)-(I=37)THENP(40)=100:PRINT"O.K.":GOTO2
358 GOTO355
359 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO361
360 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
361 IF(I=41)*(C=18)THENPRINT"HOW?(i.e. USE WHAT)":GOTO43
362 IF(I=29)*(C=36)THENPRINT"HOW?(i.e. USE WHAT)":GOTO43
363 PRINT"YOU CANNOT DO THAT":GOTO43
364 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO366
365 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
366 IF(P(9)=1)*(C=9)THENP(C)=2:PRINT"O.K.":GOTO2
367 IF(P(C)=28)*(C=14)THENP(14)=2:PRINT"O.K.":GOTO2
368 IF(P(17)=1)*(C=17)THENP(C)=2:PRINT"O.K.":GOTO2
369 PRINT"I'M CONFUSED":GOTO43
370 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO372
371 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
372 IFC=45THEN374
373 GOTO118
374 IFP(45)<>1THENPRINT"YOU DO NOT HAVE THE BAG":GOTO43
375 IF(I<>20)*(I>15)*(I<25)-(I=31)-(I=30)THENPRINT"O.K.":FB=1:GOTO2
376 PRINT"I SEE NO WATER":GOTO43
377 FORX=1TO157STEP3:IFMID$(X$,F+2,3)=MID$(B$,X,3)THENC=(X+2)/3:GOTO380
378 NEXT:PRINT"I DO NOT THINK THERE IS ONE OF THOSE":GOTO43
379 P(52)=0:P(38)=I:P(48)=I:PRINT"O.K.":GOTO2
380 IFC=27THEN382
381 GOTO118
382 IFP(27)<>ITHENPRINT"I DON'T SEE IT HERE":GOTO43
383 PRINT"O.K.":I=47:GOTO2
384 FORX=1TO16STEP3:IFMID$(X$,F+2,3)=MID$(S$,X,3)THENC=(X+2)/3:GOTO386
385 NEXT:PRINT"I DO NOT UNDERSTAND YOU":GOTO43
386 ONCGOTO388
387 PRINT"THAT WOULD DO NO GOOD":GOTO43
388 IFP(44)<>ITHENPRINT"I CAN'T SEE IT":GOTO43
389 IFP(11)<>0THENPRINT"IT'S ALREADY DEAD":GOTO43
390 IFP(42)<>1THENPRINT"YOU HAVE NO BOW":GOTO43
391 IFP(43)<>1THENPRINT"YOU HAVE NO ARROW":GOTO43
392 PRINT"O.K.":B$(44)=" A DEAD BIRD ":P(43)=0:P(11)=I:GOTO2
393 IFMID$(X$,F+2,3)="TOR"THEN395
394 NEXT:PRINT"I DO NOT UNDERSTAND YOU":GOTO43
395 IFP(20)<>1THENPRINT"YOU DO NOT HAVE THE TORCH":GOTO43
396 IFP(21)<>1THENPRINT"YOU DO NOT HAVE THE CINDER BOX":GOTO43
397 IFP(22)<>1THENPRINT"YOU DO NOT HAVE THE FLINT":GOTO43
398 IF(I=30)-(I=32)THENPRINT"NOT HERE":GOTO43
399 NL=1:B$(20)=" A LIT TORCH ":PRINT"O.K.":GOTO2
400 ONT(C)GOTO403,404,405,406,407,405,405,408,403,408,409,410
401 ONT(C)-12GOTO407,402,405,402,405,409,411,405,412
402 PRINT"IT WON'T LET YOU":GOTO43
403 PRINT"HE WON'T LET YOU":GOTO43
404 PRINT"THEY WON'T LET YOU":GOTO43
405 PRINT"DON'T BE SILLY":GOTO43
406 PRINT"YOU CAN'T THE SKELETON IS WEARING IT":GOTO43
407 PRINT"IT IS TOO HEAVY TO CARRY":GOTO43
408 PRINT"YOU AND WHO'S ARMY":GOTO43
409 PRINT"YOU CAN'T IT'S PART OF THE WALL":GOTO43
410 PRINT"TRY ANOTHER COMMAND":GOTO43
411 PRINT"THEIR TOO HEAVY TO CARRY":GOTO43
412 PRINT"THAT'S TOO DIFFICULT, BUT YOU DO NEED IT":GOTO43
413 PRINT"on barren moorland.":PRINT"There is a dark cloud overhead and you"
414 PRINT"sense evil all around you."
415 IF(I=12)-(I=8)-(I=9)THEN502
416 IF(I=4)-(I=5)THEN501
417 IFI=13THEN498
418 IFI=14THEN496
419 IFI=10THEN499
420 IFI=6THEN495
421 IFI=3THEN494
422 IFI=11THEN493
423 IFI=7THEN500
424 PRINT"in a narrow valley.":PRINT"On either side of you are steep slopes"
425 PRINT"of loose rock.":GOTO497
426 PRINT"infront of a lake.":PRINT"There are mountains on either side of"
427 PRINT"you.":GOTO498
428 PRINT"by a lake. There ":PRINT"are mountains on either side of you."
429 GOTO495
430 PRINT"by a lake. There ":PRINT"are mountains on either side of you."
431 GOTO494
432 PRINT"by a lake. There":PRINT"is a gap in the mountain range.":GOTO500
433 PRINT"by a lake. There ":PRINT"are mountains on either side of you."
434 GOTO497
435 PRINT"by a lake. There ":PRINT"is a mysterious chill in the air."
436 GOTO496
437 PRINT"in a small hollow":PRINT"there is a narrow passage leading up."
438 PRINTZ$;"WEST & UP":RETURN
439 PRINT"by a lake. There ":PRINT"is a mysterious chill in the air."
440 PRINTZ$;"EAST,SOUTH & UP":RETURN
441 PRINT"by a lake. There ":PRINT"is a stream leading north."
442 GOTO501
443 PRINT"at the end of the":PRINT"valley. There are two paths leading "
444 PRINT"up the mountain.":GOTO498
445 PRINT"on a path in a ":PRINT"mountain blizard, you can barely see."
446 GOTO497
447 PRINT"infront of an old":PRINT"stone staircase."
448 PRINTZ$;"UP & DOWN":RETURN
449 PRINT"infront of a stone":PRINT"door."
450 PRINTZ$;"DOWN":RETURN
451 PRINT"swimming in a lake.":RETURN
452 PRINT"on an island.":RETURN
453 PRINT"on the bottom of the   lake.":BH=BH+1:IFBH=5THEN567
454 PRINTZ$;"UP":RETURN
455 PRINT"in a cave.":IFNL=0THENPRINT"IT IS TOO DARK TO SEE ANY EXIT":RETURN
456 PRINTZ$;"WEST,DOWN":RETURN
457 PRINT"in a cave.":IFNL=0THENPRINT"IT IS TOO DARK TO SEE ANY EXIT":RETURN
458 PRINTZ$;"SOUTH & UP":RETURN
459 PRINT"in a cave.":IFNL=0THENPRINT"IT IS TOO DARK TO SEE ANY EXIT":RETURN
460 GOTO491
461 PRINT"in a long tunnel.":GOTO497
462 PRINT"at the bottom of a dampwell."
463 IFP(40)-100THEN488
464 PRINTZ$;"NORTH & UP":RETURN
465 PRINT"on a rock there is a tower infront of you."
466 PRINTZ$;"DOWN":RETURN
467 PRINT"in a circular room     with stone walls."
468 IFSC=1THENPRINTZ$;"SOUTH, UP & DOWN":RETURN
469 PRINTZ$;"SOUTH & DOWN":RETURN
470 PRINT"in a cellar."
471 PRINTZ$;"UP":RETURN
472 PRINT"in a circular room     there is a spiral staircase leading"
473 PRINT"down.":GOTO466
474 PRINT"on a window ledge."
475 PRINTZ$;"NO OBVIOUS EXITS":RETURN
476 PRINT"in a hole."
477 GOTO471
478 PRINT"in a pit of snakes."
479 IFNL=0THEN581
480 GOTO471
481 PRINT"in an iron maiden, there are spikes all around you."
482 IFP(9)<>2THEN577
483 GOTO488
484 PRINT"in a hollow tree, lookslike a good hiding place."
485 GOTO490
486 PRINT"in the branches of a   tree, directly above the armoured man"
487 GOTO466
488 PRINTZ$;"NORTH":RETURN
489 PRINTZ$;"SOUTH":RETURN
490 PRINTZ$;"WEST":RETURN
491 PRINTZ$;"EAST":RETURN
492 PRINTZ$;"EAST & WEST ":RETURN
493 PRINTZ$;"EAST & SOUTH":RETURN
494 PRINTZ$;"EAST & NORTH":RETURN
495 PRINTZ$;"WEST & NORTH":RETURN
496 PRINTZ$;"WEST & SOUTH":RETURN
497 PRINTZ$;"NORTH & SOUTH":RETURN
498 PRINTZ$;"EAST,WEST & SOUTH":RETURN
499 PRINTZ$;"NORTH,SOUTH & WEST":RETURN
500 PRINTZ$;"NORTH,SOUTH & EAST":RETURN
501 PRINTZ$;"EAST,WEST & NORTH":RETURN
502 PRINTZ$;"NORTH,SOUTH,WEST & EAST":RETURN
503 CONSOLE0,25:COLOR ,,4,0:PRINT" WELCOME TO 'ADVENTURE'"
504 PRINT" Written by G.Clark"
505 PRINT" Copyright SharpSoft 83": COLOR ,,7,1:GOTO11
506 PRINT[5,0]" WOULD YOU LIKE INSTRUCTIONS (Y/N) ?"
507 GETNU$:IFNU$=""THEN507
508 IFNU$="Y"THEN511
509 IFNU$="N"THEN525
510 USR(62):GOTO507
511 CONSOLE0,25:COLOR ,,6,0:PRINT"            INSTRUCTIONS"
512 PRINT"            ƒƒƒƒƒƒƒƒƒƒƒƒ"
513 PRINT" In this adventure you find yourself in"
514 PRINT"a strange land. Your object is to find"
515 PRINT"treasure and put it in the correct place"
516 PRINT"to score."
517 PRINT"You may use two word commands like :-"
518 PRINT"GO NORTH,TAKE SWORD,ENTER HOUSE etc."
519 PRINT"Other useful commands are:- SCORE"
520 PRINT"QUIT,SAVE GAME,HELP,INV(INVENTORY)"
521 PRINT"The game has a large vocabulary so try"
522 PRINT"any command which springs to mind."
523 PRINT"  €€€PRESS ANY KEY TO CONTINUE€€€": COLOR ,,7,1
524 GETNU$:IFNU$=""THEN524
525 PRINT" WOULD YOU LIKE TO RESTORE A"
526 PRINT"      PREVIOUSLY SAVED GAME (Y/N) ?"
527 GETNU$:IFNU$=""THEN527
528 IFNU$="Y"THEN531
529 IFNU$="N"THEN30
530 USR(62):GOTO527
531 PRINT"SURE YOU WANT TO RESTORE":PRINT"A GAME (Y/N) ?"
532 GETNU$:IFNU$=""THEN532
533 IFNU$="N"THEN30
534 IFNU$="Y"THEN536
535 USR(62):GOTO532
536 PRINT"  PRESS [D] WHEN DATA CASSETTE READY ?"
537 GETNU$:IFNU$=""THEN537
538 IFNU$="D"THEN540
539 USR(62):GOTO537
540 ROPEN"ADVENT DATA"
541 INPUT/TB$(1),B$(5),B$(20),B$(44)
542 FORX=1TO53:INPUT/TP(X):NEXT
543 FORX=1TO53:INPUT/TT(X):NEXT
544 INPUT/TNL,UP,D1,PL,SC,FB,N,I,BH,A,B,HM
545 CLOSE/T
546 GOTO30
547 CONSOLE0,25:PRINT"SURE YOU WANT TO SAVE":PRINT"THIS GAME (Y/N) ?"
548 GETNU$:IFNU$=""THEN548
549 IFNU$="N"THEN30
550 IFNU$="Y"THEN552
551 USR(62):GOTO548
552 PRINT"  PRESS [D] WHEN DATA CASSETTE READY ?"
553 GETNU$:IFNU$=""THEN553
554 IFNU$="D"THEN556
555 USR(62):GOTO553
556 WOPEN"ADVENT DATA"
557 PRINT/TB$(1),B$(5),B$(20),B$(44)
558 FORX=1TO53:PRINT/TP(X):NEXT
559 FORX=1TO53:PRINT/TT(X):NEXT
560 PRINT/TNL,UP,D1,PL,SC,FB,N,I,BH,A,B,HM
561 CLOSE/T
562 PRINT"DO YOU WANT TO CONTINUE":PRINT"THIS GAME (Y/N) ?"
563 GETNU$:IFNU$=""THEN563
564 IFNU$="N"THENEND
565 IFNU$="Y"THEN30
566 USR(62):GOTO563
567 CONSOLE0,25:COLOR,,2,0:PRINT"       YOUR LUNGS FILL WITH WATER"
568 PRINT"        AND YOU DROWN.":FORX=1TO3000:NEXT:GOTO600
569 CONSOLE0,25:PRINT"     SURE YOU WANT TO QUIT (Y/N) ?"
570 GETNU$:IFNU$=""THEN570
571 IFNU$="Y"THEN600
572 IFNU$="N"THEN30
573 USR(62):GOTO570
574 IFP(33)<>ITHENPRINT"I DO NOT SEE HIM":GOTO3
575 CONSOLE0,25:PRINT"   THE WIZARD DID NOT LIKE YOUR IDEA"
576 PRINT"        SO HE KILLS YOU!":FORX=1TO3000:NEXT:GOTO600
577 CONSOLE0,25:PRINT"   YOU ARE SPIKED TO DEATH INSIDE THE"
578 PRINT"        IRON MAIDEN AS YOU HAD NO"
579 PRINT"        PROTECTIVE CLOTHING   "
580 FORX=1TO3000:NEXT:GOTO600
581 CONSOLE0,25:PRINT"   THE SNAKES ATTACK YOU."
582 PRINT"    YOU ARE PARALYSED AND EATEN "
583 PRINT"        ALIVE !!!"
584 FORX=1TO3000:NEXT:GOTO600
585 CONSOLE0,25:PRINT"   YOU TRIP AND FALL DOWN"
586 PRINT"    A HOLE BREAKING YOUR NECK!"
587 FORX=1TO3000:NEXT:GOTO600
588 CONSOLE0,25:PRINT"   THE WOLF ATTACKS YOU  "
589 PRINT"    RIPPING YOUR THROAT OUT!!!"
590 FORX=1TO3000:NEXT:GOTO600
591 CONSOLE0,25:PRINT"   A HIDEOUS MONSTER RISES"
592 PRINT"   FROM THE LAKE AND HAS BREAKFAST."
593 PRINT"   YOU !!!"
594 FORX=1TO3500:NEXT:GOTO600
595 CONSOLE0,25:PRINT"   THE TORCH SETS LIGHT TO"
596 PRINT"    YOU, YOU BURN TO DEATH!!"
597 FORX=1TO3000:NEXT:GOTO600
598 CONSOLE0,25:PRINT"   YOU FREEZE TO DEATH"
599 FORX=1TO2000:NEXT:GOTO600
600 CONSOLE0,25:COLOR,,4,0:PRINT"     THE GAME IS OVER.":PRINT"     WOULD YOU LIKE TO"
601 PRINT"      TRY AGAIN (Y/N) ?"
602 GETNU$:IFNU$=""THEN602
603 IFNU$="Y"THENRUN
604 IFNU$="N"THENEND
605 USR(62):GOTO602
606 DATA" A MAN IN ARMOUR "," THREE ORCS "," A SWORD "," A STAFF "
607 DATA" A SKELETON IN CHAIN MAIL "," *A MEDALLION*"," A PIT OF SNAKES "
608 DATA" SNOW "," CHAIN MAIL "," *A SILVER GOBLET* "," *A GOLD RING* "
609 DATA" *AN IVORY HORN* "," *PLATINUM CROWN* "," OIL "," *JEWELS* "
610 DATA" A CANNON BALL "," WARM CLOTHES "," A TREASURE CHEST "," HAMMER "
611 DATA" A TORCH "," CINDER BOX "," FLINT "," WATER "," A HOLE "
612 DATA" AN ISLAND "," A SHOVEL "," TREES "," SNOW "," *DIAMOND* "
613 DATA" *ONYX CARVING* "," *GOLD STUD* ","xxx"," A WIZARD "," FIREWORKS "
614 DATA" RAGS "," STONE DOOR "," ENGRAVING "," STONE "," OLD PARCHMENT"
615 DATA" A ROPE "," IRON LADY "," A BOW "," AN ARROW "," A BLACK BIRD "
616 DATA" WATER BAG "," A WELL "," A KEY "," *SILVER BRACELET* "," A WOLF "
617 DATA" A WINDOW "," A LEVER "," THREE STONE FIGURES "," LAND "
618 DATA38,28,30,0,44,42,35,26,0,31,0,32,43,0,0,23,40,41,0,31
619 DATA46,46,30,0,30,37,38,27,45,40,0,0,16,7,18,29,40,0,41,36
620 DATA5,44,44,38,25,4,45,0,0,0,39,0,30
621 DATA1,2,0,0,0,0,3,0,4,0,0,0,0,21,0,0,0,5,0,0
622 DATA0,0,0,6,7,0,8,0,0,0,0,0,9,0,0,10,11,0,12,0
623 DATA13,0,0,14,0,15,0,0,16,17,18,19,20
