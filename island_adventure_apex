0 CLS:PRINT@168,"ISLAND ADVENTURE"
20 REM COPYRIGHT 1982                 APEX TRADING LTD.
30 CLEAR1000:DIM M(40,4),OB(40),OJ$(40),R$(40),DR$(4),OL(40)
40 RESTORE
50 READ V$,N$,RN,NN,CP
60 FOR J=1TO RN:READ R$(J)
70 FORK=1TO4:READ M(J,K):NEXT K,J
80 FOR J=1 TO NN:READ OL(J):NEXT J
90 FOR J=1TO NN:READ OJ$(J):NEXT J
100 FOR J=1 TO 4:READ DR$(J):NEXT J
110 PRINT@64,"LEGEND HAS IT THAT THERE IS A   FABULOUS TREASURE HIDDEN ON A   REMOTE ISLAND.":PRINT
120 PRINT"TOGETHER WE WILL ATTEMPT TO FINDIT. GIVE ME TWO WORD PHRASES AS INSTRUCTIONS E.G. GO NORTH,     TAKE INVENTORY, EXAMINE ROCK.":PRINT
130 PRINT"THERE ARE MANY MORE INSTRUCTIONSYOU MAY USE, BUT I WON'T SPOIL  YOUR FUN BY TELLING YOU THEM!   IN CASES OF EXTREME DESPERATION TYPE ""HELP""."
140 PRINT:PRINT@480,"PRESS ANY KEY TO START";
150 IF INKEY$="" THEN 150
160 CLS
170 GOTO280
180 IF CP=29ORCP=12 OR CP=19 THEN 1040
190 PRINT@0,"":PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
200 PRINT@32,"LOCATION: ";R$(CP)
210 FOR J=1 TO NN:IF OL(J)=CP THEN 220 ELSE NEXT J:GOTO 230
220 PRINT"VISIBLE: ";:FOR J=1 TO NN:IF OL(J)<>CP THEN NEXTJ ELSE PRINTOJ$(J):NEXTJ
230 PRINT"DIRECTIONS: ";:FOR J=1TO4:IF M(CP,J)=0 OR M(CP,J)=88 THEN NEXTJ ELSE PRINTDR$(J);:NEXTJ
240 PRINT
250 PRINTSTRING$(32,140)
260 RETURN
270 PRINT
280 GOSUB180:PRINT@480,"":INPUT"WHAT NOW";IP$
290 IF IP$="HELP"THEN 1480
300 X=INSTR(1,IP$," ")
310 VB$=LEFT$(IP$,3)
320 NT$=MID$(IP$,X+1,LEN(IP$)-X)
330 NO$=MID$(IP$,X+1,3)
340 VB=(INSTR(1,V$,VB$)+2)/3
350 IF VB<1 THEN PRINT"I DON'T UNDERSTAND THE VERB":GOTO 280
360 NO=(INSTR(1,N$,NO$)+2)/3
370 IF NO<1 THEN PRINT"I DON'T UNDERSTAND THE NOUN":GOTO 280
380 ON VB GOTO 390,430,450,890,680,470,530,580,620,800,800,650,650,890,1000,680,580,390,650,1070
390 IF NO<22 OR NO>24 THEN PRINT"YOU CAN'T DO THAT!":GOTO280
400 IF NO=22 AND CP=4 AND OB(2)=1 THEN PRINT"YOU DEFEATED THE PIRATE!":M(4,3)=5:R$(4)="A CAVE ENTRANCE":OJ$(22)="A DEAD PIRATE":GOTO280
410 IF NO=23 AND CP=30 AND OB(1)=1 THENPRINT"YOU DEFEATED THE DRAGON!!":M(30,1)=31:R$(30)="A CAVE":OJ$(23)="A DEAD DRAGON":GOTO280
420 CLS:PRINT@73,"YOU LOST!":GOTO1560
430 IF NO=14 AND OB(9)=1 THEN CLS:PRINT@69,"IT WAS POISONED!!":GOTO 1560
440 PRINT"YOU CAN'T DO THAT!":GOTO 280
450 IF NT$="NUT CRUNCHIES" AND CP=26 THEN PRINT"THE PIRATE HAS STEPPED ASIDE!":M(26,1)=27:R$(26)="A CAVE ENTRANCE":GOTO280
460 PRINT"NOTHING HAPPENED":GOTO 280
470 IF NO=16 AND OB(5)=1 AND (OB(4)=1 OR OB(7)=1) AND CP=6 THEN PRINT"THE WOLVES HAVE FLED!":M(6,1)=7:OB(4)=0:R$(6)="AT THE EDGE OF A PINE FOREST":GOTO280
480 IF NO=5 AND OB(5)=1 AND CP=28 THEN PRINT "THERE'S A CROSSBOW ON A LEDGE   AND A DEEP PIT TO THE EAST":OL(1)=28:GOTO 280
490 IF NO=16 AND OB(5)=1 AND (OB(4)=1 OR OB(7)=1) AND CP=28 THEN PRINT"THERE'S A CROSSBOW ON A LEDGE   AND A DEEP PIT TO THE EAST!":OL(1)=28:OB(7)=0:GOTO 280
500 IF NO=16 AND OB(7)=0 AND OB(4)=0 THENPRINT"THERE'S NOTHING TO BURN":GOTO 280
510 IF NO=16 AND OB(5)=1 AND (OB(7)=1 OR OB(4)=1) THEN PRINT"NOTHING HAPPENED":GOTO 280
520 PRINT"YOU CAN'T DO THAT":GOTO 280
530 IF CP=16 AND NO=20 THENPRINT"IT SAYS:LLPU ROF TTSNIAN ETCIUNORTDS":GOTO 280
540 IF CP=7 AND NO=32 THEN PRINT"IT SAYS: ONLY THOSE THAT        PERSEVERE SHALL SUCCEED":GOTO280
550 IF CP=25 AND NO=32 THEN PRINT"IT SAYS:QVMM GPS XFBQPO":GOTO 280
560 IF CP=5 AND NO=15 THENPRINT"IT SAYS THERE IS TREASURE IN A  CAVE. UNDERNEATH IT SAYS TRY NUTCRUNCHIES":GOTO280
570 PRINT"I CAN'T DO THAT!":GOTO280
580 IF NO=33 AND CP=16 THEN CLS:PRINT@73,"B O O M ! ! ":GOTO1560
590 IF NO=34 AND CP=25 THEN PRINT"A SECRET COMPARTMENT HAS OPENED AND REVEALED A SWORD!":OL(2)=25:GOTO280
600 IF NO=35 AND CP=13 THEN PRINT"A DOOR HAS OPENED!":M(13,3)=14:POKE1168,69:GOTO280
610 PRINT"NOTHING HAPPENED":GOTO280
620 IF NO=3 AND OB(3)=1 AND CP=31 THEN PRINT"IT'S UNLOCKED!":TL=1:GOTO280
630 IF NO=3 AND CP=31 THEN PRINT"WHAT KEY?":GOTO280
640 PRINT"I DON'T UNDERSTAND THAT!":GOTO280
650 IF NO>13 THENPRINT"YOU DON'T HAVE THAT":GOTO 280
660 IF OB(NO)=1 THEN OB(NO)=0:OL(NO)=CP:PRINT"YOU'VE DROPPED IT!":GOTO280
670 PRINT"YOU DON'T HAVE ";NT$:GOTO280
680 IF NO<17 OR NO>31 THENPRINT"YOU CAN'T GO THAT WAY":GOTO280
690 IF CP=7 AND NO=28 THEN FC=FC+1:IF FC<7 THEN 280 ELSE FC=0
700 IF NO=19 AND CP=4 THEN NO=30
710 IF NO=19 AND CP=11 THEN NO=31
720 IF NO=25 AND CP=6 THEN NO=28
730 IF NO=19 AND CP=26 THEN NO=28
740 IF NO=25 AND CP=6 THEN NO=28
750 IF NO=19 AND CP=13 THEN NO=30
760 IF NO<28 OR NO>31 THEN PRINT "YOU CAN'T GO THAT WAY!":GOTO 280
770 IF M(CP,NO-27)=0 THEN PRINT"YOU CAN'T GO THAT WAY":GOTO280
780 IF M(CP,NO-27)=88 THEN PRINT"YOU CAN'T GO THAT WAY, YET!":GOTO 280
790 CP=M(CP,NO-27):GOTO280
800 IF NO=21 THEN FOR J=1 TO 13:IF OB(J)=0 THEN NEXTJ:GOTO280: ELSE PRINT OJ$(J):NEXT J:GOTO280
810 IF NO=27 AND CP=31 AND TL=1 THEN PRINT"THE TREASURE IS YOURS!!":GOTO 1600
820 IF NO>13 THENPRINT"YOU CAN'T TAKE THAT!":GOTO280
830 FOR J=1 TO 15:IF OB(J)=1 THEN CN=CN+1
840 NEXTJ
850 IF CN>3 THENPRINT"YOU CAN'T CARRY ANY MORE!":CN=0:GOTO280
860 CN=0
870 IF OL(NO)=CP THEN OL(NO)=0:OB(NO)=1:PRINT"IT'S YOURS"ELSE PRINT"THERE'S NO ";NT$;" HERE!":GOTO280
880 GOTO280
890 IF OB(NO)=0 AND OL(NO)<>CP THENPRINT"YOU CAN'T EXAMINE THAT, YET!":GOTO280
900 IF NO=1 THEN PRINT"IT SAYS: REYALSNOGARD":GOTO280
910 IF NO=2 THEN PRINT"SERIAL NO: 2119562112 61518 16918120519":GOTO280
920 IF NO=4 THEN PRINT"IT'S VERY DRY":GOTO280
930 IF NO=14 THEN PRINT"IT SMELLS FUNNY!":GOTO280
940 IF NO=22 THEN PRINT"YOU'LL NEED A WEAPON TO DEFEAT  HIM!":GOTO280
950 IF NO=23 THEN PRINT"HE'S BIG AND MEAN! YOU CAN'T GETTOO CLOSE!":GOTO 280
960 IF NO=24 THEN PRINT"TOO MANY TO FIGHT!":GOTO280
970 IF NO=26 THEN PRINT"IT'S WET!":GOTO280
980 IF NO=27 THEN PRINT"IT'S TOO HEAVY TO CARRY. YOU'LL HAVE TO OPEN THE CHEST AND TAKE THE CONTENTS!":GOTO280
990 PRINT"NOTHING SPECIAL":GOTO 280
1000 IF NO=27 AND CP=31 AND TL=0 THEN PRINT"IT'S LOCKED!":GOTO280
1010 IF NO=27 AND CP=31 AND TL=1 THEN 1600
1020 IF NO=9 AND OB(9)=1 THEN PRINT"IT'S OPEN!":OB(14)=1:GOTO 280
1030 PRINT"YOU CAN'T DO THAT":GOTO280
1040 IF CP=29 THEN CLS:PRINT@66,"AAAARGH!! A BOTTOMLESS PIT!":GOTO1560
1050 IF CP=12 THEN CLS:PRINT@67,"A CAVE FULL OF PIRATES!!":GOTO1560
1060 IF CP=19 THEN CLS:PRINT@64,"AAAARGH!! YOU FELL DOWN THE     OTHER SIDE OF THE MOUNTAIN!!":GOTO1560
1070 IF (CP=2 AND NO=17)OR(CP=2 AND NO=8) THEN PRINT"YOU'VE UNCOVERED A BOX OF       MATCHES!":OL(5)=2:GOTO280
1080 PRINT"THERE'S NOTHING THERE":GOTO280
1090 DATA KILDRISAYEXAGO LIGREAPULUSETAKGETPUTDROLOOOPEWALMOVFIGLEADIG
1100 DATA CROSWOKEYDRIMATROCPINSANFLARATBUCSTOBOOWATWRIFIRBEAPATCAVGRAINVPIRDRAWOLFORSEATRENORSOUEASWESSIGCHAROPBUSGRANUT
1110 DATA 31,36,2
1120 DATA EMPTY,0,0,0,0
1130 DATA ON A BEACH,9,0,3,6
1140 DATA IN A SMALL COVE,0,0,4,2
1150 DATA AT THE ENTRANCE TO A  CAVE GUARDED BY AN EVIL LOOKING PIRATE!,0,0,88,3
1160 DATA IN A CAVE WITH WRITINGON THE WALL,0,0,0,4
1170 DATA AT THE EDGE OF A PINE FOREST.A PACK OF WOLVES GUARDS  THE ENTRANCE,88,0,2,0
1180 DATA IN A FOREST. THERE IS A SIGN ON A TREE.,8,6,7,7
1190 DATA IN A SMALL HUT,0,7,0,0
1200 DATA ON A NARROW PATH,10,2,0,0
1210 DATA AT A CROSSROADS,26,9,13,11
1220 DATA AT A CAVE ENTRANCE,0,0,10,12
1230 DATA NO,0,0,0,0
1240 DATA AT A FLAT BLANK FACE  OF A CLIFF,0,0,88,10
1250 DATA IN A TUNNEL,15,0,20,13
1260 DATA IN A TUNNEL,16,14,17,0
1270 DATA IN A CAVERN WITH A    HANGING CHAIN,0,15,0,0
1280 DATA IN A TUNNEL,18,0,0,15
1290 DATA IN A TUNNEL,0,17,19,0
1300 DATA NO,0,0,0,0
1310 DATA IN A TUNNEL,0,24,21,14
1320 DATA IN A TUNNEL,22,0,23,20
1330 DATA IN A CAVERN,0,21,0,0
1340 DATA IN A CAVERN,0,0,0,21
1350 DATA IN A TUNNEL,20,0,25,0
1360 DATA IN A CAVERN WITH A    HANGING ROPE,0,0,19,24
1370 DATA AT A CAVE ENTRANCE. A PIRATE GUARDING THE ENTRANCE    WOULD LIKE YOU TO SAY A PASSWORD,88,10,0,0
1380 DATA IN A CAVE,30,26,28,0
1390 DATA IN A VERY DARK CAVE,0,0,29,27
1400 DATA NO,0,0,0,0
1410 DATA IN THE DRAGON'S CAVE!,88,27,0,0
1420 DATA IN THE TREASURE CAVE!,0,30,0,0
1430 DATA 0,0,8,2,0,3,7,6,10,4,27,11,4,0,0,0,0,0,0,9,0,4,30,6,0,0,31,0,0,0,0,25,0,0,13,16
1440 DATA A CROSSBOW,A SWORD,A KEY,DRIFTWOOD,A BOX OF MATCHES,A ROCK,PINE CONES,SAND,A FLASK OF WATER,A DEAD RAT,AN EMPTY BUCKET,A STONE,AN OLD BOOT
1450 DATA WATER,WRITING,FIRE,BEACH,PATH,CAVE,GRASS,INVENTORY,A PIRATE,A LARGE DRAGON,WOLVES,FOREST,SEA
1460 DATA A TREASURE CHEST,N,S,E,W,A SIGN,A CHAIN,A ROPE,A BUSH,GRAFFITI
1470 DATA "N"," S"," E"," W"
1480 IF CP=2 THEN PRINT"TIME TO BUILD A SANDCASTLE!":GOTO280
1490 IF CP=6 THEN PRINT"YOU'LL HAVE TO BE BRIGHT TO GET RID OF THESE!":GOTO280
1500 IF CP=7 THEN PRINT"READ THE SIGN!":GOTO280
1510 IF CP=10 OR (CP>13 AND CP<24) THEN PRINT"I'M AS CONFUSED AS YOU ARE!":GOTO280
1520 IF CP=26 THEN PRINT"SOMEBODY, SOMEWHERE MUST KNOW   THE PASSWORD!":GOTO280
1530 IF CP=28 THEN PRINT"IT'S DARK HERE ISN'T IT?":GOTO280
1540 IF CP=13 THEN PRINT"TIME FOR A SPOT OF GARDENING!":GOTO280
1550 PRINT"DON'T FORGET TO EXAMINE THINGS!":GOTO280
1560 PRINT@163,"Y O U ' R E    D E A D !"
1570 PRINT@448,""
1580 INPUT "TRY AGAIN";Q$
1590 IF LEFT$(Q$,1)="Y" THEN 30 ELSE 1610
1600 CLS:PRINT@162,"<<THE TREASURE IS YOURS!!>>":PRINT@448,""
1610 END
