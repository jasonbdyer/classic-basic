1 HOME 
5 REM    VARIABLE NAME LISTING
10 REM    LF = LIMITING FACTOR OF DICE
15 REM    CO = CONSTITUTION
20 REM    OC = OGRE'S CONSTITUTION
25 REM    ST = STRENGTH
30 REM    OS = OGRE'S STRENGTH
35 REM    AG = AGILITY
40 REM    OA = OGRE'S AGILITY
50 REM    WI = WISDOM
55 REM    OW = OGRE'S WISDOM
60 REM    YT = YOUR TOTAL ATTRIBUTES
65 REM    OT = OGRE'S TOTAL ATTRIBUTES
70 REM    YH = YOUR HITTING THE OGRE
75 REM    OH = THE OGRE HITTING YOU
100 PRINT 
110 PRINT " ATTACK OF THE 3 TOED OGRE"
115 PRINT " BY"
120 PRINT " KEN AND MIKE ROSE"
200 REM  SET UP CONSTITUTION
205 LF = 10: GOSUB 11000:CO = X
210 PRINT: PRINT
215 PRINT "WELL, HERE YOU ARE AGAIN TRYING"
220 PRINT "TO CONQUER A TERRIBLE MONSTER." 
225 PRINT : GOSUB 10000 
230 PRINT "YOU LOOK REALLY PEPPY TODAY."
235 PRINT "I'LL BET YOU COULD FIGHT YOUR WEIGHT " 
240 PRINT "IN "; CO; " WILDCATS." 
245 PRINT : GOSUB 10000 
300 REM  THE OGRE'S CONSTITUTION 
305 LF = CO: GOSUB 11500:OC = X
310 PRINT "AH HA...HERE COMES A MONSTER." 
315 GOSUB 10000: PRINT 
320 PRINT "IT'S A THREE TOED OGRE AND LOOKS TOUGH AS "; 
325 PRINT OC; " WILDCATS.": PRINT : GOSUB 10000
330 INPUT "ARE YOU READY TO TANGLE WITH HIM? "; A$: HOME 
335 IF LEFT$(A$,1) = "N" THEN GOTO 20000 
400 REM  MUSCLES LIKE STEEL 
405 LF = 10: GOSUB 11000:ST = X
410 PRINT: PRINT
415 PRINT "HMM...LET ME FEEL YOUR MUSCLE." 
420 PRINT : GOSUB 10000 
425 PRINT "ON A SCALE OF 1 THROUGH 10, I'D SAY "
430 PRINT "YOU WERE A "; ST; "."
500 REM  THE OGRE'S STRENGTH
505 LF = ST: GOSUB 11500:OS = X
510 PRINT : GOSUB 10000
515 PRINT "OF COURSE, JUDGING BY THAT OGRE'S KNOTTY"; 
520 PRINT "ARMS, I'D SIZE HIM UP AS A "; OS; "." 
525 PRINT : GOSUB 10000 
530 PRINT "NOW LET'S SEE... THAT OGRE IS AT LEAST"
535 PRINT "AS TOUGH AS "; OC; " WILDCATS WHILE YOU CAN" 
540 PRINT "LICK "; CO; " WILDCATS.": PRINT
545 GOSUB 10000
550 INPUT "ARE YOU SURE YOU WANT TO FIGHT? "; A$: HOME 
555 IF LEFT$(A$,1) = "N" THEN GOSUB 20000 
600 REM  NIMBLE AS A GAZELLE 
605 LF = 6: GOSUB 11000:AG = X 
610 PRINT : PRINT
615 PRINT "YOU MIGHT HAVE A CHANCE AT THAT. IT"
620 PRINT "APPEARS YOU COULD LEAP "; AG; " BUILDINGS"
625 PRINT "AT A SINGLE (OR DOUBLE) BOUND."
630 GOSUB 10000
635 PRINT
700 REM  THE OGRE'S AGILITY
705 LF = AG: GOSUB 11500:OA = X
710 PRINT "OF COURSE, IT APPEARS THAT THE NIMBLE
715 PRINT "THREE TOED OGRE COULD HOP OVER AT "
720 PRINT "LEAST "; OA;" BUILDINGS."
725 PRINT : GOSUB 10000
730 PRINT "DO YOU WANT TO STOP? IT APPEARS THAT"
735 PRINT "THE TERRIBLE CREATURE IS GETTING READY
740 INPUT "TO SPRING: "; A$: HOME
745 IF LEFT$(A$,1) = "Y" THEN GOTO 20000
800  REM WISDOM OF THE AGES
805 LF = 10: GOSUB 11000:WI = X
810 PRINT : PRINT
815 PRINT "JUST ONE OTHER THING YOU'D BETTER "
820 PRINT "CONSIDER. WHILE YOU'RE AS SMART AS "
825 PRINT "AT LEAST "; WI; " FOXES, THE OGRE IS MORE"
830 PRINT "INTELLIGENT THAN ";
900 REM  THE OGRE AIN'T SO DUMB
905 LF = WI: GOSUB 11500:OW=X
910 PRINT OW; " OWLS."
915 GOSUB 10000
920 PRINT : PRINT
930 PRINT "O.K. THIS IS YOUR VERY LAST CHANCE TO "
935 INPUT "QUIT. DO YOU WANT TO QUIT? "; A$: HOME
940 IF LEFT$(A$,1) = "Y" THEN GOTO 20000
1000 REM THE  GREAT FIGHT 
1005 HOME
1010 YT = CO + ST + AG + WI
1015 OT = OC + OS + OA + OW
1020 LF = 20: REM  ROLL 20 SIDED DIE
1025 REM  OGRE'S TURN
1030 GOSUB 11000:OH = X
1035 IF OH > 10 THEN YT = YT - 1: REM  OGRE LANDS A PUNCH
1040 IF YT = 0 THEN GOTO 30000
1045 IF OH > 10 THEN PRINT "OUCH... THE OGRE LANDS A PUNCH.": PRINT "YOU'VE ONLY GOT "; YT; " POINTS LEFT.": PRINT
1050 FOR X = 1 TO 1000: NEXT X
1055 REM  YOUR TURN
1060 GOSUB 11000:YH=X
1065 IF YH > 10 THEN OT = OT - 1: REM  YOU LAND A PUNCH
1070 IF OT = 0 THEN GOTO 30000
1075 IF YH > 10 THEN PRINT "THUD...TAKE THAT MR. OGRE.": PRINT "THE MONSTER HAS ";OT; " POINTS LEFT.": PRINT 
iOSO FOR X = 1 TO 1000: NEXT X 
1085 GOTO 1025 
10000 REM  WAITING AROUND 
10005 FOR X = 1 TO 2500: NEXT : RETURN 
11000 REM  YOUR "DIE ROLLING" SUBROUTINE 
11005 X = INT ( RND (1) * LF) + 1: RETURN 
11500 REM  OGRE'S "DIE ROLLING" SUBROUTINE 
11505 X = ( RND (1) * 2)
11510 IF LF * X < LF * .75 OR LF * X > LF * 1.25 THEN GOTO 11505 
11515 X = INT (LF * X + 1): RETURN 
20000 REM  RUNNING AWAY SUBROUTINE 
20005 PRINT : PRINT
20010 PRINT "O.K. YOU YELLOW BELLIED, LILY LIVERED"
20015 PRINT "TURNIP. YOU RUN AWAY AND LIVE AGAIN" 
20020 PRINT "TO FIGHT ANOTHER DAY!!!": END 
30000 REM  TALLY UP THE RESULTS 
30005 HOME : PRINT : PRINT 
30010 IF YT = 0 THEN GOTO 30035 
30015 PRINT "CONGRATULATIONS. YOU HAVE BEATEN THE" 
30020 PRINT "TERRIBLE THREE TOED OGRE TO A PULP."
30025 PRINT "YOU TAKE HIS BAG OF GOLD AND NEW SHOES" 
30030 PRINT "AND GO WHISTLING MERRILY DOWN THE ROAD.": END 
30035 PRINT "THE THREE TOED OGRE PICKS YOU UP AND " 
30040 PRINT "OFFERS TO TAKE YOU HOME FOR DINNER."
30045 PRINT "WHICH HE DOES AND YOU ARE.": END