0 CLEAR800:M$="!€>@>€í°É"
1 A=VARPTR(M$):M1=PEEK(A+1):M2=PEEK(A+2)
2 CLS:PRINT@465,"The Programmer's Guild Presents:
                  LOST   SHIP   ADVENTURE

                    by  Charles Forsythe

        Copyright 1980 (C) (P) by The Programmer's Guild
           Unauthorized reproduction PROHIBITED"
3 FORX=1TO10:IFC=152THENC=164ELSEIFC=164THENC=139ELSEC=152
4 PRINT@0,STRING$(61,C);:PRINT@960,STRING$(61,C);:FORT=0TO15:PRINT@T*64,CHR$(C);:PRINT@T*64+60,CHR$(C);:NEXTT,X
10 CLS:R=24:O=28
11 DIMRD$(R,2),OS$(3,2),DH$(R):IFPEEK(16396)=195THEND1=1:DEFUSR0=(M2*256)+M1ELSEPOKE16526,M1:POKE16527,M2
12 DIMO$(O),P(O):FORT=1TO6:READAD$(T):NEXT:AL=70:I$="X":I=VARPTR(I$):POKEI+1,197:POKEI+2,63:FORT=1TO3:READOS$(T,1),OS$(T,2):NEXT:READVH$:GOTO7000
16 CLS:PRINT@768,CHR$(168)CHR$(176)CHR$(178)CHR$(191)CHR$(177)CHR$(176)CHR$(148)"  LOST SHIP ADVENTURE
"CHR$(172)CHR$(144)" "CHR$(191)" "CHR$(160)CHR$(156)"  BY CHARLES FORSYTHE
 "CHR$(130)STRING$(3,131)CHR$(129)
17 PRINT@0,STRING$(23,"-")"LOST SHIP ADVENTURE"STRING$(22,"-")
20 FORT=1TOR:READRD$(T,1),RD$(T,2):NEXT:FORT=1TOO:READO$(T),P(T):NEXT:FORT=1TOR:READDH$:RD$(T,2)=DH$+RD$(T,2):NEXT:Y1=1
100 PRINT@128,STRING$(255,32)"

";:PRINT@128,RD$(Y1,1)
101 PRINT@320,"YOU SEE:";:FORT=1TOO:IFP(T)<>Y1THEN103
102 FORN=7TOLEN(O$(T)):IFMID$(O$(T),N,1)="*"THENPRINT"  ";:GOTO103ELSEPRINTMID$(O$(T),N,1);:NEXTN:PRINT"  ";
103 NEXTT:IFPEEK(16416)=72THENPRINT"NOTHING SPECIAL"CHR$(30)ELSEPRINTCHR$(30)
104 PRINT@448,"OBVIOUS EXITS:";:FORT=1TO6:IFMID$(RD$(Y1,2),T,1)="1"THENPRINTAD$(T)" ";:NEXTELSENEXT
105 PRINT@512,"----------------------------------------------------------------";
110 PRINT@960,"---->";
111 IFPEEK(14502)=3THENPRINTCHR$(15);:GOTO5160ELSEPRINTCHR$(14);:FORT=1TO20:A$=INKEY$:IFA$=""THENNEXTELSE113
112 IFPEEK(14502)=3THENPRINTCHR$(15);:GOTO5160ELSEPRINTCHR$(15);:FORT=1TO20:A$=INKEY$:IFA$=""THENNEXT:GOTO111ELSE113
113 IFASC(A$)=8ANDPOS(0)<>5THEN120
114 IFA$=CHR$(13)THEN121ELSEIFA$="["THENPRINTCHR$(15);:GOTO8000
115 IFASC(A$)<32ORASC(A$)>90THEN111
116 IFPOS(0)=31THEN111ELSEIFA$=" "ANDPOS(0)=5THEN111
120 PRINTA$;:GOTO111
121 IFPOS(0)=5THEN111ELSEPOKEI,POS(0)-5:I2$=I$:PRINTCHR$(15);:GOSUB600
122 V$=LEFT$(I2$,4):IFMID$(V$,3,1)=" "ANDRIGHT$(V$,1)<>" "THENV$=LEFT$(V$,2)+"  "
123 GOSUB500
124 IFRIGHT$(I2$,1)=" "THENI2$=LEFT$(I2$,LEN(I2$)-1):GOTO124
130 FORNP=LEN(I2$)TO1STEP-1:IFMID$(I2$,NP,1)<>" "THENNEXT:N$="":NP=LEN(I2$)+1ELSEN$=MID$(I2$,NP+1,4)
131 FORT=1TOLEN(VH$)STEP6:IFV$<>MID$(VH$,T,4)THENNEXT:PRINT"I DON'T KNOW HOW TO "CHR$(34)LEFT$(I2$,NP-1)CHR$(34)" SOMETHING.";:GOSUB600:GOTO110ELSEV=VAL(MID$(VH$,T+4,2)):VN=(T-1)/6+1
133 N$=N$+STRING$(4-LEN(N$)," "):IFV=3THEN136ELSEIFVN=22THEN140
134 IFN$="    "THENN=0:GOTO140ELSEIFN$="SUIT"THENPRINT"YOU'RE WEARING IT AND YOU CAN'T REACH THE ZIPPER.";:GOSUB600:GOTO110
135 FORT=1TOO:IFN$<>LEFT$(O$(T),4)THENNEXT:PRINT"I DON'T KNOW WHAT A "CHR$(34)MID$(I2$,NP+1)CHR$(34)" IS.";:GOSUB600:GOTO110ELSEN=T:GOTO137
136 FORT=7TOLEN(RD$(Y1,2))STEP2:IFLEFT$(N$,2)=MID$(RD$(Y1,2),T,2)THENY1=VAL(MID$(RD$(Y1,2),T+2,2)):GOTO100ELSENEXT:PRINT"YOU CAN'T GO THAT WAY YET...";:GOSUB600:GOTO110
137 FORT=7TOLEN(O$(N)):IFMID$(O$(N),T,1)<>"*"THENNEXT
138 IFMID$(O$(N),T-1,1)="S"ANDMID$(O$(N),T-2,1)<>"S"THENHM=2ELSEHM=1
140 ONVGOTO 5101,5201,100,5401,5501,5600,5701,5801,5160,6001,6010,6050,6070,5000,5010,5050
500 IFY1=1ORY1>7THEN503ELSEAL=AL-1:IFAL=0THENPRINT"YOU HAVE RUN OUT OF AIR IN YOUR TANKS! YOU HAVE DROWNED";:GOSUB600:INPUT"PLAY AGAIN";A$:IFLEFT$(A$,1)="Y"THENRUNELSECLS:END
501 IFAL=1THENPRINT"YOUR AIR GAUGE READS IN THE RED!";:GOSUB600:GOTO503
502 IFAL<10THENPRINT"YOUR AIR GAUGE READS IN THE YELLOW...";:GOSUB600
503 MT=MT-1:IFMT<>0THENRETURN
504 IFP(19)=8THENP(19)=14:MT=-1:RETURN
505 IFP(11)>7ANDP(11)<14THENP(11)=-2
506 IFP(17)>7ANDP(17)<14THENP(17)=-2
507 MT=5:P(19)=8:RETURN
600 J=USR(I):PRINT@960,CHR$(30);:RETURN
990 DATA NORTH,SOUTH,EAST,WEST,UP,DOWN
995 DATA THAT'S,THOSE ARE,IS TO MUCH,ARE TOO MANY,THAT,THOSE
999 DATA GET 01DROP02GO  03EXAM04WITH05SET 06ENTE03GIVE07TAKE01PICK01CLIM03PLOT06BREA08INVE09READ04OPEN10UNLO10KILL11STAB11CUT 11STOR12SAY 11SMAS08SEAR04MARK06SWIM03CATC13FOLL03JUMP03EAT 11DIVE03QUIT14SCOR15HELP16LOWE11SAIL11SING11FEED11CHIP08
1000 DATA YOU ARE FLOATING ABOVE THE WATER NEXT TO THE SIDE OF A LARGE
SHIP. ALL THE LIFE RAFTS HAVE BEEN TAKEN AND THE SAILS NEED RE- PAIR.,DO02UP08OC02SH08,YOU ARE UNDERWATER BENEATH A SHIP OF SOME SORT,UP01SU01EA03WE04NO06SO06
1001 DATA YOU ARE UNDERWATER. YOU CAN'T SEE THE SHIP ANYMORE!,NO07SO06EA04WE02,YOU ARE UNDERWATER. YOU CAN'T SEE THE SHIP ANYMORE!,NO05SO06EA02WE07,YOU ARE UNDERWATER. YOU CAN'T SEE THE SHIP ANYMORE!,NO03SO07EA06WE05
1002 DATA YOU ARE UNDERWATER. YOU CAN'T SEE THE SHIP ANYMORE!,NO03SO05EA07WE04,YOU ARE UNDERWATER AT A CORAL REEF.,NO05SO06EA03WE04UP24,YOU ARE ON THE DECK OF THE SHIP. IT HASN'T BEEN TOUCHED IN      YEARS. THE RIGGING LOOKS O.K. THOUGH.
1003 DATA NO09ST09OC01UN02DO01,YOU ARE IN THE CAPTAIN'S STUDY. IT IS WET IN HERE. A LADDER GOESTHROUGH CEILING.,UP11SO08LA11NE11,YOU ARE IN THE MAP ROOM. THE WINDOWS LOOK OUT ONTO AN ENDLESS   OCEAN. THE SEA BREEZE COMES THROUGH THEM.
1004 DATA NO08WI12,YOU ARE IN THE CROW'S NEST. YOU SEE SOMETHING THAT MIGHT BE AN  ISLAND IN THE EAST.,DO09LA09OF05,YOU ARE ON A SECONDARY DECK IN THE STERN OF THE SHIP.THE WINDOWSOF THE MAP ROOM OVERLOOK IT. THE ENTRANCE TO THE CARGO HOLD IS  HERE.
1005 DATA DO13HO13EN13WI10RO10MA10,YOU ARE IN THE CARGO HOLD. MOST OF THE OLD CARGO HAS DECAYED.,UP12TR12,YOU ARE SWIMMING IN A SHALLOW AREA NEXT TO THE SHIP.,UP08SH08BO08NO15,"YOU ARE ON A BEACH, NEXT TO THE OCEAN. YOU CAN SEE THE SHIP.",WA14OC14NO20
1006 DATA YOU ARE IN THE JUNGLE. YOU SEE A CAVE ENTRANCE.,NO22SO20EA21WE18CA22,YOU ARE IN THE JUNGLE. YOU CAN'T FIND A PATH!,NO16SO21EA20WE21,YOU ARE IN THE JUNGLE. YOU CAN'T FIND A PATH!,NO21SO21EA17WE18,YOU ARE IN THE JUNGLE. YOU CAN'T FIND A PATH!
1007 DATA NO18SO21EA17WE20,YOU ARE ON A FAINT PATH LEADING FROM THE BEACH INTO A JUNGLE.,NO19SO15EA18WE18PA21,YOU ARE IN THE JUNGLE. YOU CAN'T FIND A PATH!,NO21SO18EA19WE17,YOU ARE IN A CAVE.,SO16EA23PA23JU16
1008 DATA YOU ARE IN A CAVE PASSAGE. IT IS HARD TO SEE.,WE22,YOU ARE ON A BEACH. YOU SEE THE SHIP IN THE DISTANCE.,OC07NO07WA07SH07
2000 DATA SHIP05SAILING SHIP*A SIGN SAYS ITS NAME IS "S.S. DEATH FLOWER",1,SUIT02DIVING SUIT*YOU ARE WEARING IT,1000,CORA05CORAL*THERE IS A STRANGE FORMATION OF IT HERE.,7,DOG 05DOG GUARDING SOUTH DOOR*IT GROWLS WHEN YOU COME NEAR. IT LOOKS HUNGRY.,8
2001 DATA BONE02BONES*THEY ARE THE CORAL ENCRUSTED BONES OF AN OLD SAILOR.,-1,FEAT02FEATHER*IT'S A PEN!,11,EGGS05GULL EGGS*NOTHING SPECIAL,11,PAPE02PAPERS*THEY USED TO BE MAPS BUT THE INK HAS RUN,10
2002 DATA MAP 06MAP ON TABLE*IT IS NAILED DOWN WITH TACKS.AN "X" MARKS THE SHIP'S POSITION.,10,LADD06LADDER*IT GOES UP THROUGH A TRAP DOOR TO THE CROW'S NEST.,9,DAGG03JEWELED DAGGER*VERY GOOD WORKMANSHIP. THE BLADE COULD CHIP THROUGH SOLID ROCK!,11
2003 DATA SACK05ROTTING SACKS*THE PUTRID SACKS LIE IN A HEAP IN ONE CORNER,13,MACH06RUSTED MACHINE*IT IS TOO CORRODED TO IDENTIFY,13,NET 02NET*SMALL NET FOR CATCHING FISH. IT SEEMS IN GOOD CONDITION.,13
2004 DATA SAIL05X*X,-1,COUR05X*X,-1,BAG 03BAG*IT'S FULL OF GOLD!!!,13,BOOK02BOOK*X,9
2005 DATA MANA05FAT MANATEE*THERE'S SOMETHING IN IT'S MOUTH,-1,PIRA05DEAD PIRATE*LET HIM REST IN PEACE!,22,ROCK05ROCK*X,23,KEY 03GOLD KEY*NOTHING SPECIAL,-2,SIGN06SIGN STUCK IN SAND*"TAKE TRESURES HERE AND SAY 'STORE'",24
2006 DATA FISH02FISHES*THEY SWIM QUICKLY,14,SAND05SAND*IT MAKES UP THE BEACH.,15,FLAG05BLACK FLAG*IT'S THE SKULL AND CROSSED BONES!,8,SEAW05SEAWEED*NOTHING SPECIAL,14,STEA02STEAK*IT'S ALL ROTTEN. NOT EVEN A DOG WOULD EAT IT!,8
3000 DATA 000011,111110,111100,111100,111100,111100,111110,100001,010010,100000,000001,000001,000010,100010,100000,111100,111100,111100,111100,111100,111100,011000,000100,100000
5000 PRINT"YOU QUIT WITH"TS"TREASURES. THAT'S "TS*25"%.";:GOSUB600:GOSUB600:PRINT@896,"";:END
5010 PRINT"YOU HAVE"TS"TREASURES STORED.";:GOSUB600:GOTO110
5050 PRINT"TRY EXAMINING THINGS.";:GOSUB600:GOTO110
5101 IFN=7ANDY1=11THENPRINT"A SEAGULL FLIES AND HITS YOU FROM BEHIND. YOU FALL INTO THE";:GOSUB600:PRINT"OCEAN AND LOSE EVERYTHING YOU ARE HOLDING.";:GOSUB600:FORT=3TOO:IFP(T)=1000THENP(T)=6:NEXT:Y1=4:GOTO100ELSENEXT:Y1=4:GOTO100
5102 IFN=19THENPRINT"IT'S TOO FAT!";:GOSUB600:GOTO110
5103 IFN=4ORN=20THEN5401
5104 IFN=21ANDLEFT$(O$(21),1)="R"THEN PRINT"IT'S A BIG ROCK!";:GOSUB600:GOTO110
5105 IFN=24ANDY1=14THENPRINT"YOU HAVE TO 'CATCH' THEM FIRST.";:GOSUB600:GOTO110
5107 IFN=26THENPRINT"IT'S UP TOO HIGH!";:GOSUB600:GOTO110
5150 IFP(N)=1000THENPRINT"YOU HAVE "OS$(3,HM)" ALL READY!";:GOSUB600:GOTO110
5151 IFP(N)<>Y1THENPRINTOS$(1,HM)" NOT HERE.";:GOSUB600:GOTO110
5152 OT=VAL(MID$(O$(N),5,2)):IFOT=5THENPRINT"THERE "OS$(2,HM)" TO GET!";:GOSUB600:GOTO110
5153 IFOT=6THENPRINTOS$(1,HM)" HELD DOWN BY SOMETHING.";:GOSUB600:GOTO110
5154 IFOQ=5THENPRINT"YOU HAVE TOO MUCH ALL READY.";:GOSUB600:GOTO110
5155 OQ=OQ+1:P(N)=1000:PRINT"YOU GET "OS$(3,HM);:GOSUB600:GOTO101
5160 PRINT"YOU HAVE:";:FORT=1TOO:IFP(T)<>1000THENNEXTT:GOTO5165
5161 FORN=7TOLEN(O$(T)):N$=MID$(O$(T),N,1):IFN$="*"THEN5170ELSEPRINTN$;
5162 IFPOS(0)=60THENGOSUB600
5163 NEXTN:NEXTT
5165 GOSUB600:GOTO110
5170 IFPOS(0)>58THENGOSUB600:NEXTT:GOTO5165ELSEPRINT"    ";:NEXTT:GOTO5165
5201 '
5251 IFP(N)<>1000THENPRINT"YOU DON'T HAVE "OS$(3,HM)"!";:GOSUB600:GOTO110
5252 OQ=OQ-1:P(N)=Y1:PRINT"YOU DROP "OS$(3,HM)".";:GOSUB600:GOTO101
5401 IFN=7THEN5101
5402 IFN=6THENO$(N)="PEN 02PEN*IT'S A PEN!":GS=1
5403 IFN=21ANDLEFT$(O$(21),1)="R"THENO$(21)="CHES02CHEST*IT'S A TRESURE CHEST. BUT IT'S LOCKED.":GS=1
5405 IFN<>18THEN5451ELSEIFP(18)<>Y1ANDP(18)<>1000THEN5410
5406 PRINT"FULL FATHOM FIVE THY FATHER LIES;";:GOSUB600:PRINT"OF HIS BONES ARE CORAL MADE;";:GOSUB600:PRINT"THOSE WERE PEARL'S THAT WERE HIS EYES.";:GOSUB600:PRINT"NOTHING OF HIME THAT DOTH FADE";:GOSUB600
5407 PRINT"BUT DOTH SUFFER A SEA CHANGE.";:GOSUB600:PRINT"INTO SOMETHING RICH AND STRANGE...";:GOSUB600:GOTO110
5451 IFP(N)<>Y1ANDP(N)<>1000THENPRINTOS$(1,HM)" NOT HERE.";:GOSUB600:GOTO110
5460 FORT=7TOLEN(O$(N)):IFMID$(O$(N),T,1)<>"*"THENNEXT
5461 FORX=T+1TOLEN(O$(N)):PRINTMID$(O$(N),X,1);:IFPOS(0)=60THEN:GOSUB600:NEXTELSENEXT
5462 GOSUB600
5463 IFGS=1THENGS=0:GOTO101ELSE110
5501 IFN=11ANDY1=7ANDP(5)=-1ANDP(11)=1000THENPRINT"YOU'VE UNCOVERED SOMETHING";:GOSUB600:P(5)=7:GOTO100
5502 IFN=14ANDP(24)<>Y1PRINT"THEY'RE NOT HERE.";:GOSUB600:GOTO110
5503 IFN=14ANDOQ=5PRINT"YOU HAVE TOO MUCH ALL READY.";:GOSUB600:GOTO110
5504 IFP(N)<>1000THENPRINT"YOU DON'T HAVE THAT.";:GOSUB600:GOTO110
5505 IFN=11ANDY1=7ANDP(N)=1000PRINT"YOU'VE BROKEN ENOUGH ALL READY.";:GOSUB600:GOTO110
5506 IFN=14THENOQ=OQ+1:P(24)=1000:PRINT"O.K.";:GOSUB600:GOTO100
5599 PRINT"IT DOESN'T WORK.";:GOSUB600:GOTO110
5600 IFN=15THEN5650
5601 IFY1<>10THENPRINT"YOU CAN'T DO THAT HERE!";:GOSUB600:GOTO110
5602 IFN<>16ANDN<>9THENPRINT"YOU CAN'T "V$" "OS$(3,HM)"!";:GOSUB600:GOTO110
5605 IFP(6)<>1000ORLEFT$(O$(6),1)="F"THENPRINT"YOU DON'T HAVE ANYTHING TO MARK THE MAP WITH!";:GOSUB600:GOTO110
5610 IFCS=0THENPRINT"YOU MARK A COURSE TO A NEAR ISLAND IN THE EAST.";:GOSUB600:CS=1:O$(9)="MAP 06MAP ON TABLE*STUCK DOWN WITH TACKS. IT NOW HAS A COURSE PLOTTED.":GOTO110
5611 PRINT"YOU'VE DONE THAT ALL READY.";:GOSUB600:GOTO110
5650 IFY1<8ORY1>13THENPRINT"YOU CAN'T DO THAT HERE!";:GOSUB600:GOTO110ELSEIFCS=0THENPRINT"YOU HAVE NO COURSE!";:GOSUB600:GOTO110
5651 IFCS=1THENCS=2:MT=10:P(19)=15
5652 IFCS=2THENRD$(8,2)="110001DO14OC14NO09SO10WA14ST09":P(7)=-1:CS=3:PRINT"O.K.";:GOSUB600:P(1)=14:GOTO110
5653 IFCS=3THENRD$(8,2)="110001NO09ST09UN02DO01OC01RO10SO10":P(7)=11:CS=2:PRINT"O.K.";:P(1)=1:GOSUB600:GOTO110
5701 IFY1<>8THEN5750
5702 IFP(N)<>1000THENPRINT"YOU DON'T HAVE "OS$(3,HM)".";:GOSUB600:GOTO110
5703 IFN<>5THENPRINT"OK... NOTHING HAPPENS.";:OQ=OQ-1:GOSUB600:P(N)=Y1:GOTO101
5704 P(5)=-1:PRINT"THE DOG TAKES THE BONES AND RUNS OFF SOMEWHERE.";:OQ=OQ-1:GOSUB600:P(4)=-1:RD$(8,2)="110001NO09ST09UN02OC01RO10SO10DO01":GOTO100
5750 IFN<>24ORY1<>P(19)ORP(N)<>1000THEN5799
5753 OQ=OQ-1:PRINT"THE MANATEE TAKES THE FISH. WHEN HE OPENS HIS MOUTH,SOMETHING";:GOSUB600:PRINT"FALLS OUT.";:GOSUB600:FORT=1TOO:IF P(T)=-2 THENP(T)=Y1:NEXT ELSENEXT
5754 P(19)=-1:P(24)=-1:PRINT"THE MANATEE GOES OFF";:GOSUB600:GOTO100
5799 IFP(N)<>1000THENPRINT"YOU DON'T HAVE "OS$(3,HM)".";:GOSUB600:GOTO110ELSEPRINT"OK... NOTHING HAPPENS.";:OQ=OQ-1:GOSUB600:P(N)=Y1:GOTO101
5801 IFN=7THEN5101
5802 IFN=3ANDY1=7THENPRINT"WITH WHAT, I.E. "CHR$(34)"WITH FEATHER"CHR$(34);:GOSUB600:GOTO110
5804 IFN=3THENPRINT"IT'S NOT HERE.";:GOSUB600:GOTO110
5899 PRINT"YOU CAN'T BREAK THAT.";:GOSUB600:GOTO110
6001 IFN<>21THENPRINT"YOU CAN'T OPEN "OS$(3,HM);:GOSUB600:GOTO110
6002 IFMID$(O$(21),7,1)="O"THENPRINT"IT'S OPEN ALL READY!";:GOSUB600:GOTO110
6003 IFP(21)<>1000ANDP(21)<>Y1 THENPRINT"IT'S NOT HERE.";:GOSUB600:GOTO110
6004 IFP(22)<>1000THENPRINT"IT'S LOCKED AND YOU DON'T HAVE ANYTHING TO OPEN IT WITH!";:GOSUB600:GOTO110
6005 PRINT"O.K.";:GOSUB600:O$(21)="CHES03OPEN CHEST*IT'S FULL OF GEMS.":GOTO100
6010 IFVN=18ORVN=19ORVN=20THENPRINT"DON'T BE SO DESTRUCTIVE!";:GOSUB600:GOTO110
6011 IFVN<>22THEN6015ELSEPRINTCHR$(34);:FORX=1TOLEN(I2$):IFMID$(I2$,X,1)<>" "THENNEXT:PRINTCHR$(34);:GOSUB600:GOTO110
6012 FORT=X+1TOLEN(I2$):PRINTMID$(I2$,T,1);:NEXT:PRINTCHR$(34);:GOSUB600:GOTO110
6015 IFVN=30ANDN=28ANDP(28)=1000THENP(N)=-1:PRINT"YOU FEEL SICK.";:GOSUB600:GOTO101
6016 IFVN=30ANDN=28ANDP(28)=Y1THENP(28)=-1:PRINT"YOU FEEL SICK.";:GOSUB600:GOTO101
6017 IFVN=35ANDY1=8THENPRINT"THERE IS NO ROPE.";:GOSUB600:GOTO110
6018 IFVN=35THENPRINT"THERE IS NOTHING TO LOWER HERE.";:GOSUB600:GOTO110
6019 IFVN=36THENN=15:GOTO5600
6021 IFVN=37THENPRINT"LA LA LA DEE DA...";:GOSUB600:GOTO110
6022 IFVN=30ANDN=28THENPRINT"THAT'S NOT HERE!";:GOSUB600:GOTO110
6023 IFVN=30THENPRINT"IT WOULD BE FOOLISH TO EAT "OS$(3,HM)"!";:GOSUB600:GOTO110
6024 IFVN=38ANDY1=8ANDP(4)=8THENPRINT"IF HE'S HUNGRY 'GIVE' HIM SOMETHING.";:GOSUB600:GOTO110
6025 IFVN=38ANDY1=P(19)THENPRINT"IF IT'S HUNGRY 'GIVE' IT SOMETHING.";:GOSUB600:GOTO110
6026 PRINT"THERE'S NOTHING TO 'FEED' HERE.";:GOSUB600:GOTO110
6050 IFY1<>24THENPRINT"YOU CAN'T DO THAT HERE.";:GOSUB600:GOTO110ELSE6054
6051 OQ=OQ-1:TS=TS+1:P(N)=-1:PRINT"SCORE:"TS"TREASURES.";:GOSUB600:GOTO110
6054 IFN=21THEN6060
6055 IFN=0THENPRINT"STORE WHAT?";:GOSUB600:GOTO110
6056 IFVAL(MID$(O$(N),5,2))<>3THENPRINTOS$(1,HM)" NOT A TREASURE!";:GOSUB600:GOTO110
6057 IFP(N)<>1000THENPRINT"YOU CAN'T STORE SOMETHING YOU DON'T HAVE!";:GOSUB600:GOTO110
6060 IFMID$(O$(21),7,1)<>"O"THENPRINT"YOU'RE NOT SURE IT'S A TREASURE, IT COULD BE FULL OF DIRT!";:GOSUB600:GOTO110 ELSE6051
6070 IFN=17THEN PRINT"IT'S TOO BIG TO CATCH.";:GOSUB600
6071 IFN=4THENPRINT"YOU TRY BUT IT BITES YOU.";:GOSUB600:GOTO110
6072 IFN<>24THENPRINT"YOU CAN'T CATCH "OS$(3,HM)".";:GOSUB600:GOTO110
6073 IFY1<>P(24)THENPRINT"THOSE ARE NOT HERE.";:GOSUB600:GOTO110
6074 PRINT"WITH WHAT I.E. WITH FEATHER.";:GOSUB600:GOTO110
7000 PRINTSTRING$(14,176):PRINTCHR$(191)"1)NEW GAME  "CHR$(191):PRINTCHR$(191)"2)SAVED GAME"CHR$(191):PRINTSTRING$(14,131)
7001 A$=INKEY$:IFA$=""THEN7001ELSEIFA$="2"THEN8500ELSEIFA$<>"1"THEN7001
7002 CLS:PRINT"             LOST SHIP ADVENTURE
WHILE DIVING FOR TREASURE IN THE CARRIBEAN, YOU COME ACROSS A   LONG LOST PIRATE SHIP, ADRIFT FOR YEARS. TO MOVE YOURSELF AND   MANIPULATE OBJECTS, USE TWO WORD COMMANDS SUCH AS:'GET BOX',    'DROP CAT'";
7003 PRINT". JUST SAY WHAT SOUNDS SENSIBLE. TO GET A LIST OF WHATYOU HAVE, SAY 'INVENTORY' OR PRESS -SHIFT- -I-. PRESS -[- TO    SAVE THE GAME ON TAPE. GOOD LUCK!
                 -PRESS ENTER-"
7004 IFINKEY$<>CHR$(13)THEN7004ELSE16
8000 GOSUB600:PRINT"PREPARE TO FOR SAVING.";:GOSUB600:A$=INKEY$:FORT=1TO500:NEXT:PRINT"AND PRESS ENTER.";:GOSUB600
8001 IFINKEY$<>CHR$(13)THEN8001ELSEIFD1=1THENCMD"T
8002 PRINT #-1,O$(6),O$(9),O$(21),RD$(8,2):FORT=1TOOSTEP4:PRINT#-1,P(T),P(T+1),P(T+2),P(T+3):NEXT:PRINT#-1,OQ,Y1,AL,CS,MT,TS:RUN
8500 PRINT"PREPARE TAPE FOR LOADING OF DATA, AND THEN PRESS -ENTER-."
8501 IFINKEY$<>CHR$(13)THEN8501
8502 FORT=1TOR:READ RD$(T,1),RD$(T,2):NEXT:FORT=1TOO:READO$(T),P:NEXT:FORT=1TOR:READDH$:RD$(T,2)=DH$+RD$(T,2):NEXT:IF D1=1 THENCMD"T
8503 INPUT#-1,O$(6),O$(9),O$(21),RD$(8,2):FORT=1TOOSTEP4:INPUT#-1,P(T),P(T+1),P(T+2),P(T+3):NEXT:INPUT#-1,OQ,Y1,AL,CS,MT,TS:CLS
8504 PRINT@0,"]"STRING$(22,"-")"LOST SHIP ADVENTURE"STRING$(21,"-")"^":GOTO100