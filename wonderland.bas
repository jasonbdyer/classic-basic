100 REM * WONDERLAND * 4K COLOR BASIC OR LEVEL II BASIC *
110 CLS
120 PRINT "W O N D E R L A N D"
130 CLEAR 250
140 DATA WALRUS,MAD HATTER, WHITE RABBIT,CHESHIRE CAT,RED QUEEN
150 DATA JABBERWOCKY,TWEEDLEDUM,TWEEDLEDEE,WHITE KNIGHT,ALICE
160 DATA RABBIT HOLE,TEA PARTY,POOL OF TEARS,OYSTER BED
170 DATA QUEEN'S THRONE,THE WABE,EIGHTH SQUARE
180 DATA GLASS TABLE,LONG HALL,CROQUET GROUND
190 G$="WRONG. EXIT WONDERLAND"
200 DIM D$(25),E$(5,5)
210 FOR C=1 TO 25
220 IF C>20 THEN D$(C)="CLUE": GOTO 250
230 READ D$(C)
240 IF C<11 THEN D$(C)=" "+D$(C)
250 NEXT C
260 S=RND(10)
270 K$=RIGHT$(D$(S),LEN(D$(S))-1)
280 A=1
290 B=1
300 FOR C=1 TO 25
310 D=RND(25)
320 IF D$(D)="" GOTO 310ELSE E$(A,B)=D$(D): D$(D)=""
330 IF B<6 THEN B=B+1
340 IF B=6 THEN B=1: A=A+1
350 IF A=6 THEN A=1
360 NEXT C
370 A=RND(5)
380 B=RND(5)
390 PRINT STRING$(20,"*")
400 X$=E$(A,B)
410 PRINT "LOCATION: ";X$
420 IF L$=X$ THEN PRINT K$;" FOUND. YOU WIN": END
430 IF LEFT$(X$,1)=" " AND Z$<>K$ GOSUB 510
440 IF X$="CLUE" GOSUB 590
450 INPUT "DIRECTION";D$
460 IF D$="N" AND B>1 THEN B=B-1
470 IF D$="S" AND B<5 THEN B=B+1
480 IF D$="W" AND A>1 THEN A=A-1
490 IF D$="E" AND A<5 THEN A=A+1
500 GOTO 390
510 INPUT "MYSTERY GUESS Y/N";T$
520 IF T$<>"Y" AND T$<>"N" GOTO 510
530 IF T$="N" RETURN
540 PRINT "CLUE LETTERS: ";M$
550 INPUT "GUESS";Z$
560 IF Z$=K$ GOTO 870
570 IF Z$<>K$ THEN PRINT G$: END
580 RETURN
590 PRINT
600 FOR T=1 TO RND(500)+500
610 NEXT T
620 CLS
630 FOR T=1 TO RND(500)+500
640 NEXT T
650 G=RND(20)
660 H=RND(20)
670 PRINT H"+"G
680 FOR T=1 TO 60
690 NEXT T
700 CLS
710 PRINT @ 0,"ANSWER";
720 INPUT S
730 IF S=H+G THEN PRINT "RIGHT": PRINT "NEW LETTER IS ";
740 IF S<>H+G THEN PRINT "WRONG": RETURN
750 Q$=MID$(K$,RND(LEN(K$)),1)
760 PRINT Q$
770 M$=M$+Q$
780 PRINT "IF YOU HAVE 13 CLUE LETTERS, YOU LOSE."
790 Y=LEN(M$)
800 PRINT "CLUE LETTERS: ";M$;" NUMBER:";LEN(M$)
810 IF Y=13 THEN PRINT G$: END
820 F=RND(5)
830 I=RND(5)
840 E$(A,B)=E$(F,I)
850 E$(F,I)="CLUE"
860 RETURN
870 PRINT "RIGHT."
880 PRINT "NOW FIND ";K$
890 L$=" "+K$
900 GOTO 390
