1 REM TRS80 POLYNESIAN ADVENTURE
5 REM FROM THE RAINBOW BOOK OF ADVENTURES
6 REM TYPED IN BY SIMON FOR CASA
7 REM Sound commands ignored for sanity preservation!
10 GOTO1530
20 FORAA=1TO200:NEXT
30 CLS
40 PRINT"YOU ARE ";
50 PRINTR$(L,0)
60 PRINT:PRINT"OBVIOUS EXITS"
70 FOR D=1TOLEN(R$(L,1))/2 STEP 2
80 IF MID$(R$(L,1),D,2)="N " THENPRINT"NORTH ";
90 IF MID$(R$(L,1),D,2)="S " THENPRINT"SOUTH ";
100 IF MID$(R$(L,1),D,2)="E " THENPRINT"EAST ";
110 IF MID$(R$(L,1),D,2)="W " THENPRINT"WEST ";
120 NEXT D:PRINT:PRINT
130 PRINT "YOU SEE"
140 FORJ=1TONJ
150 X$=RIGHT$(J$(J),2):IF VAL(X$)=L THENPRINTLEFT$(J$(J),LEN(J$(J))-2)
160 NEXTJ
170 PRINTSTRING$(32,"=")
210 PRINT"by your command-->"
220 INPUT C$
230 C$=C$+"     "
240 P=INSTR(C$," ")
250 V$=LEFT$(C$,3)
260 N$=MID$(C$,P+1,3)
270 V=INSTR(AV$,V$)
280 IFV=0THENPRINT"SORRY I DON'T KNOW THAT VERB":GOTO 210
290 N=INSTR(AN$,N$)
300 IFN=0THEN PRINT"SORRY I DON'T KNOW THAT NOUN":GOTO 210
310 V=(V+2)/3
320 ON V GOTO 340,340,340,340,410,420,490,530,550,590,630,660,680,710,720,730,800
330 GOTO210
340 P=INSTR(R$(L,1),LEFT$(V$,2))
350 IFP=0THENPRINT"WRONG WAY CHARLIE":GOTO 210
360 L=VAL(MID$(R$(L,1),P+LEN(R$(L,1))/2,2))
370 IFL<>9THEN30
380 IFS1>14THENS1=1
390 MID$(R$(6,1),5,2)=MID$(S$,S1,2):S1=S1+2
400 GOTO30
410 P=INSTR(R$(L,1),LEFT$(N$,2)):GOTO350
420 IF C>4THENPRINT"YOU'RE TOO WEAK TO CARRY ANYMORE":GOTO210
430 W=VAL(MID$(NL$,N,3)):IFW=0THENPRINT"I CAN'T GET THAT":GOTO210
440 K=LEN(J$(W))-1:Z=VAL(MID$(J$(W),K,2)):IFZ<>L THENPRINT"IT'S NOT HERE":GOTO210
450 IFL=27ANDN=34ANDMID$(J$(8),12,1)<>"9"THENPRINT"A VOICE SAYS, WHERE'S MY COINS?":GOTO210
460 IFL=27ANDN=34THEN MID$(J$(8),12,2)="00":C=C-1
470 IFN=40ANDMID$(J$(10),16,1)="V"THENPRINT"IT'S TOO hot.":GOTO210
480 MID$(J$(W),K,2)="99":PRINT"OK":C=C+1:GOTO30
490 W=VAL(MID$(NL$,N,3)):IF RIGHT$(J$(W),2)<>"99"THENPRINT"YOU DON'T HAVE IT, TURKEY":GOTO210
500 W$=STR$(L):K=LEN(J$(W))-1:MID$(J$(W),K,2)=RIGHT$(W$,2):C=C-1
510 IFL=1THENMID$(J$(1),11,1)="0":GOTO550
520 GOTO30
530 IFN=7ANDRIGHT$(J$(5),2)="00"THENPRINT"YOU FOUND SOMETHING":K=LEN(J$(5))-1:MID$(J$(5),K,2)="33":GOTO20
540 GOTO30
550 W=0:FORJ=1TONJ:IFRIGHT$(J$(J),3)="* 1"THENW=W+10
560 NEXTJ:PRINT"OUT OF 100 POINTS YOU HAVE"W
570 IFW=100THENPRINT"YOU HAVE WON":END
580 GOTO210
590 IFL=2ANDN=46ANDFL=1THENFM=1:GOTO840
600 IFL=17ANDRIGHT$(J$(2),2)="99"THENWT=1:GOTO620
610 IFL<>3ORRIGHT$(J$(2),2)<>"99"THENPRINT"YOU'RE MISSING SOMETHING":GOTO210
620 FL=1:MID$(J$(2),11,7)="(full) ":PRINT"OK":GOTO210
630 IFL<>33ORFM<>1THENPRINT"I CAN'T DO THAT":GOTO 210
640 L=TM:IFL=5THENTM=2ELSETM=5
650 MID$(J$(6),15,2)=STR$(L):MID$(R$(33,1),3,2)=STR$(L):GOTO30
660 IFRIGHT$(J$(5),2)="99"THENPRINT"YOUR CABIN NUMBER IS G7 AND YOUR TABLE NUMBER IS A1":GOTO210
670 PRINT"I DON'T HAVE THAT TO READ":GOTO210
680 PRINT "YOU ARE CARRYING"
690 FORJ=1TONJ:IFRIGHT$(J$(J),2)="99"THENPRINTLEFT$(J$(J),LEN(J$(J))-2)
700 NEXTJ:GOTO210
710 IF L>9 THENPRINT"IT IS UNLAWFUL TO PICK UP FLOWERS  YOU ARE SENT BACK TO THE BOAT":L=6:GOTO20
720 IF L>9 THENPRINT"STING!!!":PRINT"OUCH,!#$%&' YOU HAVE BEEN STUNG BY A BEE. YOU ARE RUSHED BACK TO THE BOAT FOR MEDICAL TREATMENT":L=6:GOTO20
722 PRINT"I DON'T UNDERSTAND"
725 GOTO210
730 AA=RND(5)
740 ON AA GOTO750,760,770,780,790
750 PRINT"FILL CAN & TANK OF CAR":GOTO210
760 PRINT"LOOK COMPARTMENT":GOTO210
770 PRINT"READING IS GOOD FOR PASSES":GOTO210
780 PRINT"GO BOAT SOMETIMES WORKS":GOTO210
790 PRINT"PICK & SMELL BUT LOOK OUT":GOTO210
800 IFWT=0ANDFL=1THENPRINT"THAT WOULD WASTE GAS":GOTO210
810 IFWT=0THEN PRINT "THERE IS NOTHING TO POUR":GOTO210
820 IFL<>29THEN840
830 MID$(J$(10),16,9)="cold)    ":GOTO840
840 PRINT"OK":MID$(J$(2),11,7)="(empty)":GOTO30
850 L=1:NJ=25:TM=5:CLS:SW$="05"
860 DIM R$(33,1),J$(NJ)
870 CLS:PRINT "Polynesian Adventure"
880 PRINT "BY Don Dunlap";
890 R$(1,0)="AT A POLYNESIAN TREASURE HOUSE":R$(1,1)="S 02"
900 R$(2,0)="ON A ROAD":R$(2,1)="N E W CA01030433"
910 R$(3,0)="AT GAS STATION":R$(3,1)="W 02"
920 R$(4,0)="IN A DISCOUNT STORE":R$(4,1)="E 02"
930 R$(5,0)="ON A PIER NEXT TO A BOAT":R$(5,1)="S CA0633"
940 R$(6,0)="ON A LOVE BOAT":R$(6,1)="N W 0507"
950 R$(7,0)="IN THE DINING ROOM":R$(7,1)="W A1E 100806"
960 R$(8,0)="AT THE TABLE":R$(8,1)="S 07"
970 R$(9,0)="IN CABIN G7":R$(9,1)="S 10"
980 R$(10,0)="IN A HALLWAY FULL OF CABINS":R$(10,1)="E G70709"
990 R$(11,0)="IN A SAMOAN VILLAGE":R$(11,1)="BOS E 061213"
1000 R$(12,0)="IN A COUNCIL HOUSE":R$(12,1)="N 11"
1010 R$(13,0)="IN A COMMUNITY HOUSE":R$(13,1)="W 11"
1020 R$(14,0)="IN A MAORI VILLAGE":R$(14,1)="BOS E 061615"
1030 R$(15,0)="IN A MEETING HOUSE":R$(15,1)="W 14"
1040 R$(16,0)="IN A MAORI MUSEUM":R$(16,1)="N E 1417"
1050 R$(17,0)="BY A LAGOON":R$(17,1)="W 16"
1060 R$(18,0)="IN A FIJI VILLAGE":R$(18,1)="BOS E 061920"
1070 R$(19,0)="IN A CHIEF'S HOUSE":R$(19,1)="N 18"
1080 R$(20,0)="IN A FIJIAN HUT":R$(20,1)="W 18"
1090 R$(21,0)="IN A TAHITIAN VILLAGE":R$(21,1)="BOS E 062223"
1100 R$(22,0)="IN A PRAYER HOUSE":R$(22,1)="N 21"
1110 R$(23,0)="IN A FISHERMAN'S HOUSE":R$(23,1)="W 21"
1120 R$(24,0)="IN A TONGA VILLAGE":R$(24,1)="BOS E 062625"
1130 R$(25,0)="AT A TONGAN FESTIVAL":R$(25,1)="W 24"
1140 R$(26,0)="IN A QUEEN'S BEDROOM":R$(26,1)="N E 2427"
1150 R$(27,0)="IN A QUEEN'S BED":R$(27,1)="W 26"
1160 R$(28,0)="IN A MARQUESAS VILLAGE":R$(28,1)="BOS 0629"
1170 R$(29,0)="IN A COOKING HOUSE (ha'e tumay)":R$(29,1)="N E 2830"
1180 R$(30,0)="IN A GUEST HOUSE (ha'e manahi't)":R$(30,1)="N W 3129"
1190 R$(31,0)="IN A TATTOOING HOUSE":R$(31,1)="S E 3032"
1200 R$(32,0)="IN A WARRIOR'S HOUSE (ha'e toa)":R$(32,1)="W 31"
1210 R$(33,0)="IN A TRANS AM CAR":R$(33,1)="OU02"
1220 J$(1)="NOTHING  01"
1230 J$(2)="A GAS CAN (empty)04"
1240 J$(3)="*SILVERWARE*08"
1250 J$(4)="A GLOVE COMPARTMENT33"
1260 J$(5)="A BOARDING PASS00"
1270 J$(6)="A TRANS AM CAR02"
1280 J$(7)="*GOLD NECKLACE*09"
1290 J$(8)="TONGA COINS20"
1300 J$(9)="*VALUABLE RELICS*16"
1310 J$(10)="*A GOLD KNIFE (very hot)*29"
1320 J$(11)="*EXQUISITE CARVING OF A FISH*23"
1330 J$(12)="*BEAUTIFUL WOVEN MAT*27"
1340 J$(13)="A PINK HIBISCUS11"
1350 J$(14)="*A BASKET FULL OF PEARLS*12"
1360 J$(15)="A FIRE KNIFE DANCER13"
1370 J$(16)="A COLORFUL RED TULIP TREE14"
1380 J$(17)="A BEAUTIFUL PLUMERIA18"
1390 J$(18)="*A DIAMOND HEADED SPEAR*19"
1400 J$(19)="*A HAWAIIAN ORCHID*21"
1410 J$(20)="A TROPICAL WATERFALL24"
1420 J$(21)="A BIRD OF PARADISE26"
1430 J$(22)="FREE HULA LESSONS25"
1440 J$(23)="*AN EMERALD STATUE*32"
1450 J$(24)="AN ACTIVE VOLCANO28"
1460 J$(25)="A GROUP OF BOY SCOUTS SINGING22"
1470 AV$="N  S  E  W  GO GETDROLOOSCOFILDRIREAINVPICSMEHELPOU"
1480 AN$="   CANCOMPASCAROUTG7 A1 NECCOIRELMATFISKNISILTANBOAPEASPEORCSTAHIBTULPLUPAR"
1490 NL$="000002000005000000000000007008009012011010003000000014018019023000000000000"
1500 S$="11141821242805":S1=1
1520 GOTO30
1530 CLEAR5000:GOTO850
1600 REM COPYRIGHTED 1983
1610 REM BY DON & LINDA DUNLAP
1620 REM 6840 TANYA TERRACE
1630 REM REYNOLDSBURG, OHIO
