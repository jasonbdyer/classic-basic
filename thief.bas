0 'It Takes a Thief (disk version)
5 'Copyright 1981 Randy Dobkin
10 CLS:PRINT@462,CHR$(23)"It Takes a Thief
20 CLEAR300:RANDOM:DEFINTA-Z
30 GOTO10000
40 DR$="NSEWUD":R=1:TR=1:GOSUB50:GOTO200
50 CLS:IFOB(14,0)<>RANDOB(14,0)<>0PRINT"It's too dark to see!":GOTO130ELSEPRINTR$(R):PRINT
60 IFRO(R)=0THEN100
70 PRINT"Around me I see:  ";:FORO=1TONO:IFOB(O,0)<>RNEXT:GOTO95
80 IFLEN(OB$(O))+POS(0)>63PRINT
90 PRINTOB$(O)"   ";:NEXT
95 PRINT:PRINT
100 PRINT"Directions I can go:  ";
110 FORD=1TO6:IFE(R,D)>0PRINTD$(D)"  ";
120 NEXT:PRINT
130 PRINTSTRING$(64,"=");:RETURN
200 C$="":F$="":S$="":INPUT"What should I do";C$
210 IFLEN(C$)=0THEN200ELSEFORX=1TOLEN(C$):M$=MID$(C$,X,1):IFM$=" "THEN230
220 F$=F$+M$:NEXT
230 IFLEN(F$)=LEN(C$)THEN300ELSES$=RIGHT$(C$,LEN(C$)-LEN(F$)-1)
300 FORF=1TONF:IFF$(F)<>F$NEXTELSE310
305 FORD=1TO6:IFF$=MID$(DR$,D,1)THENS$=D$(D):GOTO320ELSENEXT
307 PRINT"I don't know how to do that.":GOTO200
310 ONFGOTO320,350,370,395,400,410,440,460,470,510,520,570,580,590,620,650,350,510
320 FORD=1TO6:IFD$(D)<>S$NEXT:PRINT"Go where?":GOTO200
330 IFE(R,D)=0PRINT"I can't go that way.":GOTO200
340 IFR=12ANDD=2ANDDR=0PRINT"The door is locked.":GOTO200ELSER=E(R,D):GOSUB50:GOTO1000
350 GOSUB996
355 IFOB(O,0)=0PRINT"I already have it.":GOTO200
360 IFOB(O,0)<>RTHEN997
365 IFNC=6PRINT"I can't carry anything else.":GOTO200
367 OB(O,0)=0:NC=NC+1:RO(R)=RO(R)-1:GOTO999
370 NU=0:PRINT"I am carrying:  ";:FORO=1TONO:IFOB(O,0)<>0NEXT:IFNU=0PRINT"nothing":GOTO200ELSEPRINT:GOTO200
380 NU=1:IFLEN(OB$(O))+POS(0)>63PRINT
390 PRINTOB$(O)"   ";:NEXT:PRINT:GOTO200
395 GOSUB50:GOTO1000
400 YS=0:FORO=1TONO:IFOB(O,0)=TROROB(O,0)=0LETYS=YS+OB(O,1)
405 NEXT:PRINT"You have"YS"out of a possible"HS"points.":GOTO200
410 GOSUB996
420 IFOB(O,0)<>0THEN998
430 OB(O,0)=R:NC=NC-1:RO(R)=RO(R)+1:GOTO999
440 IFH$(R)=""PRINT"I don't know what to do.":GOTO200
450 PRINTH$(R):GOTO200
460 INPUT"Are you sure";A$:IFA$="NO"THEN200ELSEEND
470 GOSUB996
480 IFO<>2ANDO<>15THEN995
490 IFO=15THEN500ELSEIFOB(2,0)<>0THEN998ELSEIFG=1PRINT"It's all used up.":GOTO200ELSEIFOB(7,0)<>0THEN9000ELSEIFR=17LETDG=1:G=1:PRINT"Rover passes out.":GOTO1000ELSE999
500 IFOB(15,0)=0LETS=1:GOSUB999:GOTO1000ELSE998
510 IFS$<>"ALARM"THEN995ELSEIFR<>13THEN997ELSEIFAL=1PRINT"I already did.":GOTO200ELSEIFOB(20,0)<>0PRINT"I can't right now.":GOTO1000ELSEAL=1:GOTO999
520 IFS$<>"FILING CABINET"ANDS$<>"SAFE"ANDS$<>"DOOR"ANDS$<>"BOX"THEN995
530 IFS$<>"DOOR"THEN540ELSEIFR=19THEN991ELSEIFR<>12THEN997ELSEIFOB(20,0)<>0THEN994ELSEIFDR=1THEN991ELSEIFAL=0THEN9030ELSEDR=1:GOTO999
540 IFS$<>"BOX"THEN550ELSEIFR<>26THEN997ELSEIFBO=1THEN991ELSEIFS=0THEN9020ELSEFORO=8TO10:OB(O,0)=26:NEXT:BO=1:GOTO993
550 IFS$<>"SAFE"THEN560ELSEIFOB(13,0)<>0ANDOB(13,0)<>RTHEN997ELSEIFSO=1THEN991ELSEINPUT"What is the combination";CN$:IFCN$<>CB$PRINT"It doesn't open.":GOTO200ELSEOB(16,0)=R:SO=1:GOTO993
560 IFR<>19THEN997ELSEIFOB(20,0)<>0THEN994ELSEIFCO=1THEN991ELSEOB(24,0)=19:CO=1:GOTO993
570 GOSUB996:IFOB(O,0)<>0ANDOB(O,0)<>RTHEN997ELSEIFO<>2ANDO<>24PRINT"It looks like an ordinary "S$".":GOTO1000ELSEIFOB$(24)=S$THEN575ELSEPRINT"Writing on label says, "CHR$(34)"CAUTION:  knockout gas."CHR$(34):GOTO1000
575 PRINT"Writing on paper says, "CHR$(34)CB$"."CHR$(34):GOTO1000
580 GOSUB996:IFOB(17,0)<>0PRINT"I don't have my pistol.":GOTO1000ELSEIFSH=1PRINT"I'm out of bullets.":GOTO200ELSEIFO<>19PRINT"I don't want to.":GOTO200ELSEIFOB(18,0)<>0THEN9050ELSEOB$(19)="DEAD NEIGHBOR":SH=1:NB=3:GOTO993
590 IFR<>1PRINT"First I need a car.":GOTO200ELSECLS:PRINT"I got away in my getaway car.":YS=0:FORO=1TONO:IFOB(O,0)=TROROB(O,0)=0LETYS=YS+OB(O,1)
600 NEXT:PRINT"You got"YS"out of a possible"HS"points.
610 IFYS=0ENDELSEFORX=0TO10:READT$(X):NEXT:PRINT"You have earned the rank of "T$(YS/HS*10)".":PRINT:END
620 OPEN"O",1,S$:FORX=1TO25:PRINT#1,OB(X,0):NEXT
630 PRINT#1,AL,BO,CO,CB$:PRINT#1,DG,DM,DR,FL,G,LG,LT,NB,PR,R,RV,S,SH,SO:CLOSE
640 PRINT"OK":GOTO200
650 OPEN"I",1,S$:FORX=1TO25:INPUT#1,OB(X,0):NEXT
660 INPUT#1,AL,BO,CO,CB$:INPUT#1,DG,DM,DR,FL,G,LG,LT,NB,PR,R,RV,S,SH,SO:CLOSE
670 GOSUB10500:GOSUB50:GOTO200
991 PRINT"It's already open.":GOTO200
992 GOSUB10500:GOSUB50:RETURN
993 GOSUB10500:GOSUB50:GOTO1000
994 PRINT"I can't; it's locked!":GOTO200
995 PRINT"I can't "F$" that.":GOTO200
996 FORO=1TONO:IFOB$(O)<>S$NEXT:PRINTF$" what?":GOTO200ELSERETURN
997 PRINT"I don't see it here.":GOTO200
998 PRINT"I don't have it.":GOTO200
999 PRINT"OK
1000 IFR<>17ORDG=1THEN1030ELSERV=RV+1:IFRV=1PRINT"Rover is growling.
1010 IFRV=2PRINT"Rover is snarling.
1020 IFRV=3THEN9040
1030 IFOB(3,0)=0ANDOB(4,0)=0ANDOB(5,0)=0LETOB(7,0)=0:FORX=3TO5:OB(X,0)=-1:NEXT:NC=NC-2
1040 IFDM=0ANDOB(23,0)=0LETOB(20,0)=12:GOSUB992:DM=1
1050 IFPR=0ANDOB(22,0)=0LETOB(13,0)=16:GOSUB992:PR=1
1060 IFLG=0IFOB(14,0)<>0ANDOB(14,0)<>RLETLG=1
1070 IFLG>0IFOB(14,0)<>0ANDOB(14,0)<>RLETLG=LG+1:IFLG=3THEN9010
1080 IFNB=0ANDR=7LETNB=1
1090 IFNB>0ANDR=7LETNB=NB+1:IFNB=3THEN9060
1100 FL=FL+1:IFFL=115LETLT=1
1110 IFLT=1IFOB(14,0)=0OROB(14,0)=RPRINT"My flashlight went dead.":FORT=1TO1000:NEXT:IFOB(14,0)=RTHENOB(25,0)=R:OB(14,0)=-1:GOTO993ELSEOB(25,0)=0:OB(14,0)=-1:GOTO993
8999 GOTO200
9000 CLS:PRINT"I awaken in jail.
I wonder what happened!":PRINT:END
9010 CLS:PRINT"B U M P !
I must have hit something!":GOTO9100
9020 CLS:PRINT"S Q E A K !
The thing needed oil!":GOTO9100
9030 CLS:PRINT"R R R I N G !
The alarm went off!":GOTO9100
9040 CLS:PRINT"W O O F !
Goddamn Rover!":GOTO9100
9050 CLS:PRINT"B A N G !
That gun's pretty loud!":GOTO9100
9060 CLS:PRINT"O H   N O !
The neighbor ran off to use the phone!
9100 PRINT"I've been caught.
The police are taking me away.":PRINT:END
10000 NR=30:DIMR$(NR):DIMRO(NR):DIME(NR,6):DIMH$(NR)
10001 R$(1)="I'm in my getaway car.":H$(1)="If I have the stuff, let's go!":DATA0,2,0,0,0,0
10002 R$(2)="I'm on the driveway.":DATA1,11,3,10,0,0
10003 R$(3)="I'm in the front yard.":DATA0,12,4,2,0,0
10004 R$(4)="I'm at the northeast corner of the house.":DATA0,5,0,3,0,0
10005 R$(5)="I'm in a side yard.":DATA4,6,0,0,0,0
10006 R$(6)="I'm at the southeast corner of the house.":DATA5,0,0,7,0,0
10007 R$(7)="I'm in the back yard.":H$(7)="Guns are loud unless . . .":DATA0,0,6,8,0,0
10008 R$(8)="I'm at the southwest corner of the house.":DATA9,0,7,0,0,0
10009 R$(9)=R$(5):DATA10,8,0,0,0,0
10011 R$(10)="I'm at the northwest corner of the house.":DATA0,9,2,0,0,0
10013 R$(11)="I'm outside the garage.
Columns lead up to the roof.":DATA2,0,12,10,13,0
10015 R$(12)="I'm standing on the front porch.
There's a door to the south.":H$(12)="I need keys for three things.":DATA3,21,4,11,0,0
10017 R$(13)="I'm on the roof.
There's an alarm here.":DATA0,0,0,0,0,11
10019 R$(14)="I'm in the garage.":H$(14)="WD-40's good for unsqueaking.":DATA0,18,0,0,0,0
10021 R$(15)="I'm in the dining room.":DATA16,17,20,18,0,0
10023 R$(16)="I'm in the living room.":DATA0,15,21,0,0,0
10025 R$(17)="I'm in the back room.":H$(17)="I know how to make a gas mask!":DATA15,0,19,0,0,0
10027 R$(18)="I'm in the utility room.":DATA14,0,15,0,0,0
10029 R$(19)=R$(17)+"
I see a filing cabinet.
A door leads outside.":DATA20,7,0,17,0,0
10031 R$(20)="I'm in the kitchen.":DATA21,19,22,15,0,0
10033 R$(21)="I'm in the foyer.
A door leads outside.":DATA12,20,0,0,0,0
10035 R$(22)="I'm in the hall.":DATA24,23,25,20,0,0
10037 R$(23)="I'm in a bedroom.
Someone is sleeping here.":DATA22,0,0,0,0,0
10039 R$(24)=R$(23):DATA0,22,0,0,0,0
10041 R$(25)=R$(22):DATA27,26,28,22,0,0
10043 R$(26)="I'm in a big bedroom.
Two people are sleeping here.
I also see a box.":DATA25,0,0,30,0,0
10045 R$(27)=R$(23):DATA0,25,0,29,0,0
10047 R$(28)="I'm in a bathroom.":DATA0,0,0,25,0,0
10049 R$(29)="I'm in a closet.":DATA0,0,27,0,0,0
10051 R$(30)=R$(28):DATA0,0,26,0,0,0
10100 NF=18:DIMF$(NF)
10110 F$(1)="GO":F$(2)="TAKE":F$(3)="INVENTORY":F$(4)="LOOK":F$(5)="SCORE":F$(6)="DROP":F$(7)="HELP":F$(8)="QUIT":F$(9)="SPRAY":F$(10)="DEACTIVATE":F$(11)="OPEN":F$(12)="EXAMINE":F$(13)="SHOOT":F$(14)="DRIVE":F$(15)="SAVE":F$(16)="LOAD":F$(17)="GET"
10120 F$(18)="DISARM"
10200 NO=25:DIMOB$(NO):DIMOB(NO,1)
10202 OB$(1)="GRILL":DATA7,0
10204 OB$(2)="AEROSOL CAN":DATA14,0
10206 OB$(3)="CHARCOAL":DATA7,0
10208 OB$(4)="COFFEE CAN":DATA20,0
10210 OB$(5)="HALLOWEEN MASK":DATA29,0
10212 OB$(6)="ROVER":DATA17,0
10214 OB$(7)="GAS MASK":DATA-1,0
10216 OB$(8)="DIAMOND RING":DATA-1,100
10218 OB$(9)="PEARL NECKLACE":DATA-1,90
10220 OB$(10)="RUBY BRACELET":DATA-1,85
10222 OB$(11)="TELEVISION":DATA17,70
10224 OB$(12)="VIDEO TAPE RECORDER":DATA17,95
10226 OB$(13)="SAFE":DATA-1,0
10228 OB$(14)="FLASHLIGHT":DATA1,0
10230 OB$(15)="WD-40":DATA14,0
10232 OB$(16)="CASH":DATA-1,75
10234 OB$(17)="PISTOL":DATA1,0
10236 OB$(18)="SILENCER":DATA18,0
10238 OB$(19)="NOSY NEIGHBOR":DATA7,0
10242 OB$(20)="KEYS":DATA-1,0
10244 OB$(21)="MINK BATHROBE":DATA30,110
10246 OB$(22)="PAINTING":DATA16,95
10248 OB$(23)="DOORMAT":DATA12,0
10250 OB$(24)="PAPER":DATA-1,0
10260 OB$(25)="DEAD FLASHLIGHT":DATA-1,0
10300 D$(1)="NORTH":D$(2)="SOUTH":D$(3)="EAST":D$(4)="WEST":D$(5)="UP":D$(6)="DOWN"
10400 FORR=1TONR:FORD=1TO6:READE(R,D):NEXTD,R
10410 FORO=1TONO:READOB(O,0),OB(O,1):HS=HS+OB(O,1):NEXT
10420 GOSUB10500
10430 FORX=1TO5:C$=STR$(RND(50)):CB$=CB$+RIGHT$(C$,LEN(C$)-1):IFX<>5LETCB$=CB$+"-":NEXT
10440 DATARogue,Rogue,Footpad,Cutpurse,Robber,Burglar,Filcher,Sharper,Magsman,Thief,Master Thief
10450 GOTO40
10500 NC=0:FORRM=1TONR:RO(RM)=0:NEXT:FORO=1TONO:J=OB(O,0):IFJ<0THEN10510ELSEIFJ=0NC=NC+1ELSERO(J)=RO(J)+1
10510 NEXT:RETURN
